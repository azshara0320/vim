" Vim global plugin for code comment
" Maintainer: Azshara <azshara.filter@outlook.com>
" Version:
" Last Change:

if exists('g:loaded_code_comment')
    finish
endif

let g:loaded_code_comment = 1

let s:save_cpo = &cpo
set cpo&vim

" å®šä¹‰å˜é‡ {{{

let s:NOTE_ON = 1
let s:NOTE_OFF = 2
let s:NOTE_EMPTY = 4
let s:A_BLANK_SPACE = "\x20"
let s:DEFAULT_DELIMITER = ','
let s:DEFAULT_S_SYMBOL = '//'
let s:DEFAULT_D_SYMBOL = '/*'. s:DEFAULT_DELIMITER .'*/'
let s:INLINE_PREFIX = '*'. s:A_BLANK_SPACE

" æ³¨é‡Šæ¨¡å¼çš„æ¶ˆæ¯æ ‡è®°
"
" s:FLAG_DEFAULT    é»˜è®¤
" s:FLAG_BLOCK      åˆ—å—
" s:FLAG_EACH       ä¼ªæ¨¡å¼è¡¨ç¤ºåŒç¬¦å·åº”ç”¨äºæ¯ä¸€è¡Œ
" s:FLAG_FIRST      ä¼ªæ¨¡å¼è¡¨ç¤ºç¬¬ä¸€åˆ—æ’å…¥åŒç¬¦å·
" s:FLAG_SEMI       ä¼ªæ¨¡å¼è¡¨ç¤ºé¦–å’Œå°¾ç¬¦å·è¡Œæ˜¯æ–°å»ºè¡Œ
" s:FLAG_REVERSE    ä¼ªæ¨¡å¼è¡¨ç¤ºå•ç¬¦å·åè½¬
"
let s:FLAG_DEFAULT = 'Default'
let s:FLAG_BLOCK   = 'Block'
let s:FLAG_EACH    = 'Each'
let s:FLAG_FIRST   = 'First'
let s:FLAG_SEMI    = 'Semi'
let s:FLAG_REVERSE = 'Resver'
let s:presetFlag = s:FLAG_DEFAULT


" å’Œé”™è¯¯æ¶ˆæ¯ç›¸å…³çš„å®šä¹‰
"
" s:FATAL   å…¨å±€å˜é‡æ£€æµ‹çš„è‡´å‘½(Fatal)é”™è¯¯å¹¶ç»ˆæ­¢è„šæœ¬
" s:WARNING ç”±ç”¨æˆ·åœ¨æ“ä½œä¸­äº§ç”Ÿçš„è­¦å‘Š(Warning)é”™è¯¯å¹¶ç»ˆæ­¢æ³¨é‡Šæ“ä½œ
" s:NOTE    TODO
" s:DEBUG   ä»…å†…éƒ¨è°ƒè¯•ä½¿ç”¨
"
let s:FATAL = [
    \ 'FA1: ç¼ºçœæ³¨é‡Šç¬¦å·ä¸åŒ¹é…',
    \ 'FA2: å†…è¡Œå‰è¾è®¾ç½®ä¸åŒ¹é…',
    \ 'FA3: ç¬¬ä¸€åˆ—æ¨¡å¼çš„å­—ç¬¦ä¸²å€¼ä¸åŒ¹é…',
    \ 'FA4: æ¯ä¸€è¡Œæ¨¡å¼çš„å­—ç¬¦ä¸²å€¼ä¸åŒ¹é…',
\ ]

let s:WARNING = [
    \ 'WA1: å¤šä½™çš„æ³¨é‡Šç¬¦å·è¡Œ',
    \ 'WA2: æ²¡ç”¨å¯ç”¨çš„åŒç¬¦å·',
\ ]

let s:DEBUG = [
    \ 'DE1: åˆ—å—æ¨¡å¼æœ«è¡Œçš„æœ€å°ç©ºç™½å®½åº¦ä¸åŒ¹é…',
    \ 'DE2: åˆ—å—æ¨¡å¼çš„é¦–æˆ–å°¾ç¬¦å·è¡Œä¸æ˜¯ä»¥é¦–æˆ–å°¾ç¬¦å·ç»“å°¾',
    \ 'DE3: åˆ—å—æ¨¡å¼çš„å†…è¡Œå‰è¾å­—ç¬¦ä¸åŒ¹é…',
    \ 'DE4: åˆ—å—æ¨¡å¼çš„å†…è¡Œæœ€å°ç©ºç™½å®½åº¦ä¸åŒºé…',
\ ]

let s:ERROR_CODE = {
    \ 'Fatal'   :   s:FATAL,
    \ 'Warning' :   s:WARNING,
    \ 'Debug'   :   s:DEBUG,
\ }
"}}}

" åˆå§‹åŒ– {{{
"
" Default separator
if exists('g:reset_default_delimiter')
    let s:isDelimiter = g:reset_default_delimiter
    let s:DEFAULT_D_SYMBOL = '/*'. s:isDelimiter .'*/'
else
    let s:isDelimiter = s:DEFAULT_DELIMITER
endif

" Default single symbol
if exists('g:reset_default_single_symbol')
    let s:isDefinedS = g:reset_default_single_symbol

    if stridx(s:isDefinedS, s:isDelimiter) != -1
        echomsg s:ERROR_LEVEL.Fatal[0]
        finish
    endif
else
    let s:isDefinedS = s:DEFAULT_S_SYMBOL
endif

" Default double symbol
if exists('g:reset_default_double_symbol')
    let s:isDefinedD = g:reset_default_double_symbol

    if stridx(s:isDefinedD, s:isDelimiter) == -1
        echomsg s:ERROR_LEVEL.Fatal[0]
        finish
    endif
else
    let s:isDefinedD = s:DEFAULT_D_SYMBOL
endif

" æ˜¯å¦å…³é—­æ¶ˆæ¯å›æ˜¾
if exists('g:close_echo_message') && g:close_echo_message
    let s:isMsg = 0
else
    let s:isMsg = 1
endif

" å½“æ¶ˆæ¯å›æ˜¾æ˜¯å¯ç”¨çŠ¶æ€æ—¶æ£€æµ‹æ˜¯å¦ä½¿ç”¨è‡ªå®šä¹‰æ¶ˆæ¯
if s:isMsg
    if exists('g:custom_echo_message')
        \ && !empty(g:custom_echo_message)
        \ && len(g:custom_echo_message) == 2

        let s:isCustom = g:custom_echo_message
    else
        let s:isCustom = ['Cancle!', 'Done!']
    endif
endif

" æ˜¯å¦å…³é—­ä¸€ä¸ªç©ºæ ¼
if exists('g:close_strict_space') && g:close_strict_space
    let s:isBlank = "\x0"
else
    let s:isBlank = s:A_BLANK_SPACE
endif

" æ˜¯å¦ä½¿ç”¨ç¬¬ä¸€åˆ—æ¨¡å¼çš„æ–‡ä»¶ç±»å‹
if exists('g:allow_first_column_mode')
    let s:pattern = '^\(\([^,]\+\),\?\)\+[^, ]$'

    if matchstr(g:allow_first_column_mode, s:pattern) == ""
        echomsg s:ERROR_LEVEL.Fatal[2]
        finish
    endif

    let s:isFirstInsert = 1
    unlet s:pattern
else
    let s:isFirstInsert = 0
endif

" æ˜¯å¦ä½¿ç”¨æ¯ä¸€è¡Œæ¨¡å¼çš„æ–‡ä»¶ç±»å‹
if exists('g:allow_each_line_mode')
    let s:pattern = '^\(\([^,]\+\),\?\)\+[^, ]$'

    if matchstr(g:allow_each_line_mode, s:pattern) == ""
        echomsg s:ERROR_LEVEL.Fatal['3']
        finish
    endif

    let s:isEachLineInsert = 1
    unlet s:pattern
else
    let s:isEachLineInsert = 0
endif

" æ˜¯å¦ä½¿ç”¨åŠæˆå“åˆ—å—æ¨¡å¼çš„æ–‡ä»¶ç±»å‹
if exists('g:allow_semi_block_mode')
    let s:pattern = '^\(\([^,]\+\),\?\)\+[^, ]$'

    if matchstr(g:allow_semi_block_mode, s:pattern) == ""
        echomsg s:ERROR_LEVEL.Fatal['3']
        finish
    endif

    let s:isSemiBlock = 1
    unlet s:pattern
else
    let s:isSemiBlock = 0
endif

" æ˜¯å¦å¯ç”¨å•ç¬¦å·åè½¬
if exists('g:open_single_reverse') && g:open_single_reverse
    let s:isReverse = 1
else
    let s:isReverse = 0
endif

" æ˜¯å¦å¯ç”¨åˆ—å—æ¨¡å¼
if exists('g:open_column_block_mode') && g:open_column_block_mode
    let s:presetFlag = s:FLAG_BLOCK
    let s:isColumnBlockMode = 1
else
    let s:isColumnBlockMode = 0
endif

" å†…è¡Œå‰è¾åº”è¯¥åªæœ‰åœ¨å¯ç”¨äº†åˆ—å—æ¨¡å¼æ‰å¯ä»¥è®¾ç½®ï¼Œä¸è¿‡ä¸ºäº†å…¼å®¹åœ¨é»˜è®¤æ¨¡å¼çš„å–æ¶ˆæ“ä½œ
" ä¸­å¯ä»¥å–æ¶ˆåˆ—å—æ¨¡å¼çš„æ³¨é‡Šæ ¼å¼ï¼Œåªèƒ½è·³å‡ºåˆ—å—æ¨¡å¼çš„æ£€æµ‹ã€‚
if exists('g:d_line_prefix')
    if g:d_line_prefix == ""
        let s:isLinePrefix = g:d_line_prefix
    else

        if matchstr(g:d_line_prefix, '^\S') != ""
            let s:isLinePrefix = g:d_line_prefix
        else
            echomsg s:ERROR_LEVEL.Fatal[1]
            finish
        endif

    endif
else
    let s:isLinePrefix = s:INLINE_PREFIX
endif

if exists('g:method_synopsis')
    let s:isSummary = g:method_synopsis
else
    let s:isSummary = ['@return void']
endif

"}}}

" æ˜ å°„ {{{

" Single
if !hasmapto('<Plug>CommentSingleToggle')
    nmap <silent> <unique> <Leader>cc <Plug>CommentSingleToggle
endif

nnoremap <silent> <unique> <script> <Plug>CommentSingleToggle <SID>SingleToggle
nnoremap <SID>SingleToggle :<C-U>call <SID>SingleToggle(0, 0)<CR>

if !hasmapto('<Plug>CommentHighSingleToggle')
    vmap <silent> <unique> <Leader>cc <Plug>CommentHighSingleToggle
endif

vnoremap <silent> <unique> <script> <Plug>CommentHighSingleToggle <SID>HighSingleToggle
vnoremap <SID>HighSingleToggle :<C-U>call <SID>HighSingleToggle()<CR>

" Double
if !hasmapto('<Plug>CommentDoubleToggle')
    nmap <silent> <unique> <Leader>dd <Plug>CommentDoubleToggle
endif

nnoremap <silent> <unique> <script> <Plug>CommentDoubleToggle <SID>DoubleToggle
nnoremap <SID>DoubleToggle :<C-U>call <SID>DoubleToggle(0, 0)<CR>

if !hasmapto('<Plug>CommentHighDoubleToggle')
    vmap <silent> <unique> <Leader>dd <Plug>CommentHighDoubleToggle
endif

vnoremap <silent> <unique> <script> <Plug>CommentHighDoubleToggle <SID>HighDoubleToggle
vnoremap <SID>HighDoubleToggle :<C-U>call <SID>HighDoubleToggle()<CR>

" Empty note
if !hasmapto('<Plug>CommentEmptyNote')
    imap <silent> <unique> <C-L>x <Plug>CommentEmptyNote
endif

inoremap <silent> <unique> <script> <Plug>CommentEmptyNote <SID>EmptyNote
inoremap <SID>EmptyNote :<C-U><C-R>=<SID>EmptyNote(0, 0)<CR>
"}}}

" å‘½ä»¤ {{{

if !exists(':SToggle')
    command -range SToggle call <SID>SingleToggle(<line1>, <line2>)
endif

if !exists(':DToggle')
    command -range DToggle call <SID>DoubleToggle(<line1>, <line2>)
endif
"}}}

" s:SingleToggle() {{{

" å•ç¬¦å·æ“ä½œå¼€å…³
" @param number sLine å‘½ä»¤è¡Œä¸­èŒƒå›´çš„å¼€å§‹è¡Œå·
" @param number eLine å‘½ä»¤è¡Œä¸­èŒƒå›´çš„ç»“æŸè¡Œå·
" @return void
"
function s:SingleToggle(sLine, eLine)
    " key share
    if exists('b:doubleSymbol')
        call <SID>DoubleToggle(a:sLine, a:eLine)
        return
    endif

    let saCursor = getpos('.')

    " key share
    if !exists('b:singleSymbol')
        if !exists('b:symbol')
            let b:symbol = <SID>ParseCommentSymbol()
        endif

        let index = stridx(b:symbol[0], s:isDelimiter)

        " ['å•ç¬¦å·']
        if index == -1
            let sS = b:symbol[0]
        else
            " ['é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
            let b:doubleSymbol = [
                \strpart(b:symbol[0], 0, index),
                \strpart(b:symbol[0], index + 1),
            \]

            " å½“åªæœ‰ä¸€ç§åŒç¬¦å·ä¸ºæ³¨é‡Šç¬¦å·æ—¶,å…ˆæ‰§è¡Œå•ç¬¦å·æŒ‰é”®æ“ä½œæ—¶è¿™é‡Œå°†æ‰§è¡Œï¼Œ
            " å¹¶ç›´æ¥è°ƒç”¨åŒç¬¦å·æ“ä½œï¼ŒåŒæ—¶å°†åŒç¬¦å·å­˜å…¥ç¼“å†²å˜é‡ b:doubleSymbol
            " å¹¶åœ¨åŒæ“ä½œè°ƒç”¨çš„å‡½æ•°ä¸­æ£€æŸ¥æ­¤å˜é‡ä»¥èŠ‚çœå¯¹æ³¨é‡Šç¬¦å·çš„åˆ†æçš„æ­¥éª¤ã€‚å½“
            " å†æ¬¡ä½¿ç”¨å•æ“ä½œæ—¶ä¹Ÿä¼šæ£€æŸ¥ b:doubleSymbol å˜é‡ï¼Œå¹¶ç›´æ¥è°ƒç”¨åŒæ“ä½œï¼Œ
            " è¿™æ—¶å•æ“ä½œä¹ŸèŠ‚çœäº†å¯¹æ³¨é‡Šç¬¦å·åˆ†æçš„æ­¥éª¤ã€‚
            call <SID>DoubleToggle(a:sLine, a:eLine)
            return
        endif
    else
        let sS = b:singleSymbol
    endif

    " @var number é»˜è®¤è¡Œå·
    let curNo = saCursor[1]

    " @var number é»˜è®¤å¾ªç¯ç•Œé™
    let limit = curNo + 1

    " @var number æœ€å¤§è¡Œå·
    let lastNo = line('$')

    " command line
    if a:sLine || a:eLine
        let curNo = a:sLine
        let endNo = a:eLine
        let limit = endNo + 1
    endif

    " Key count
    if v:count > 1
        let limit = curNo + v:count

        if (limit - 1) > lastNo
            let limit  = lastNo + 1
        endif
    endif
    let curNo = <SID>GetRealCurrentLine(curNo, limit)

    if !curNo | return | endif

    if !exists('b:singleLen')
        let b:singleLen = strlen(sS)
    endif

    " è°ƒç”¨æ ¸å¿ƒæ“ä½œ
    let result = <SID>SingleExecCore(curNo, limit, sS)

    if s:isMsg
        call <SID>SingleEcho(curNo, sS, result)
    endif

    call setpos('.', saCursor)
endfunction
"}}}

" s:DoubleToggle() {{{

" å®šä¹‰åŒç¬¦å·æ³¨é‡Šå¼€å…³
" @param number sLine å‘½ä»¤è¡Œä¸­èŒƒå›´çš„å¼€å§‹
" @param number eLine å‘½ä»¤è¡Œä¸­èŒƒå›´çš„ç»“æŸ
" @return void
"
function s:DoubleToggle(sLine, eLine)
    if exists('b:singleSymbol')
        call <SID>SingleToggle(a:sLine, a:eLine)
        return
    end

    let saCursor = getpos(".")

    if !exists('b:doubleSymbol')
        if !exists('b:symbol')
            " ['å•ç¬¦å·']
            " ['é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
            " ['å•ç¬¦å·', 'é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
            "
            " @var list è¿”å›çš„å›ºå®šæ ¼å¼å¦‚ä¸Š
            let b:symbol = <SID>ParseCommentSymbol()
        endif

        " @var number -1 è¡¨ç¤ºä¸å­˜åœ¨åˆ†éš”ç¬¦å·
        let index = stridx(b:symbol[0], s:isDelimiter)

        " ['é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
        if index > -1
            " @var string dS é¦–ç¬¦å·
            " @var string dE å°¾ç¬¦å·
            let dS = strpart(b:symbol[0], 0, index)
            let dE = strpart(b:symbol[0], index + 1)
        else
            " ['å•ç¬¦å·', 'é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
            if index == -1 && len(b:symbol) == 2
                let index = stridx(b:symbol[1], s:isDelimiter)

                let dS = strpart(b:symbol[1], 0, index)
                let dE = strpart(b:symbol[1], index + 1)
            else
                " ['å•ç¬¦å·']
                let b:singleSymbol = b:symbol[0]
                call <SID>SingleToggle(a:sLine, a:eLine)
                return
            endif
        endif
    else
        let dS = b:doubleSymbol[0]
        let dE = b:doubleSymbol[1]
    endif

    let curNo = saCursor[1]
    let endNo = saCursor[1]
    let lastNo = line('$')

    " @var bool çœŸå€¼è¡¨ç¤ºå‘½ä»¤è¡Œçš„å•è¡Œæ“ä½œ
    let exMutilFlag = 0

    if a:sLine || a:eLine
        let curNo = a:sLine
        let endNo = a:eLine

        if a:sLine == a:eLine
            let exMutilFlag = 1
        endif
    elseif v:count > 1
        let endNo = curNo + v:count - 1

        if endNo > lastNo
            let endNo = lastNo
        endif
    endif

    if !exists('b:dHeadLen')
        let b:dHeadLen = strlen(dS)
        let b:dTailLen = strlen(dE)
    endif

    if !exists('b:prefixLen')
        let b:prefixLen = len(s:isLinePrefix)
    endif

    let curStr = getline(curNo)

    " @var string dHeadChar é¦–å­—ç¬¦
    " @var string dTailChar å°¾å­—ç¬¦
    let dHeadChar = <SID>GetHeadChar(curStr, b:dHeadLen)
    let dTailChar = <SID>GetTailChar(curStr, dE)

    let isPattFirst = <SID>IsPatternFirstMode()
    let isPattSemi = <SID>IsPatternSemiBlockMode()
    let isPattEach = <SID>IsPatternEachLineMode()

    let originalMessageFlag = s:presetFlag

    " å¤šè¡Œæ“ä½œæ˜¯ç®€å•ï¼Œåœ¨å¤šè¡Œæ“ä½œä¸­æœ‰æ˜ç¡®èŒƒå›´çš„å¼€å§‹è¡Œå·å’Œç»“æŸè¡Œå·ã€‚å¦‚æœå¼€å§‹è¡Œå­˜
    " åœ¨é¦–ç¬¦å·å¹¶ä¸”ç»“æŸçš„è¡Œå­˜åœ¨å°¾ç¬¦å·ï¼Œé‚£å°±æ˜¯å–æ¶ˆæ³¨é‡Šçš„æ“ä½œï¼Œå¦åˆ™å°±æ˜¯æ³¨é‡Šæ“ä½œ (
    " å¼ºç¡¬æ³¨é‡Š)ã€‚å•è¡Œæ“ä½œæ¯”å¤šè¡Œæ“ä½œè¦å¤æ‚äº›ï¼Œå°¤å…¶å½“å®ƒä»¬çš„åŒç¬¦å·è¿˜æ˜¯ç›¸åŒæ—¶ã€‚
    if (v:count > 1 || a:sLine || a:eLine) && !exMutilFlag
        let dTailChar = <SID>GetTailChar(getline(endNo), dE)

        if isPattEach
            let curNo = <SID>GetRealCurrentLine(curNo, endNo + 1)
            if !curNo | return | endif

            let pattern = '\V\^\s\*'. escape(dS, '\/') .'\.\*'
            let pattern .= dE .'\s\*\$'
            " æ¯ä¸€è¡Œæ¨¡å¼ä¸å…¼å®¹å…¶å®ƒæ¨¡å¼çš„å¤šè¡Œå–æ¶ˆæ£€æµ‹éœ€è¦é‡å®šä¹‰
            let each = matchstr(getline(curNo), pattern) != ""
        endif

        if dHeadChar == dS && dTailChar == dE || exists("each") && each
            let l:args = [curNo, endNo, dS, dE, saCursor]
            let lines = <SID>CompatCancel(l:args)
        else
            let isNote = s:NOTE_ON

            let multiMode = s:isColumnBlockMode || isPattFirst || isPattSemi

            if multiMode && !isPattEach
                call <SID>CreateDoubleColBlock(curNo, endNo, dS, dE, saCursor)
                let endNo += 2
            else
                if isPattEach
                    let isNo = <SID>CreateDoubleEachLine(curNo,endNo+1,dS,dE)
                    if isNo | let endNo = isNo | endif
                else
                    let cC = <SID>DoubleEX(curNo, endNo, dS, dE, s:NOTE_ON)
                    execute cC[0]
                    execute cC[1]
                endif
            endif
        endif
    else
        """ å•è¡Œæ“ä½œ

        " å½“é¦–å’Œå°¾ç¬¦å·å®Œå…¨ç›¸åŒæ—¶ï¼Œå¦‚ python çš„ "''','''"ã€‚å¹¶ä¸”åªæœ‰é¦–æˆ–å°¾ç¬¦å·çš„
        " æ˜¯ç‹¬å è¡Œæ—¶ï¼Œå®ƒä¸çŸ¥é“è¿™æ˜¯å–æ¶ˆèŒƒå›´çš„å¼€å§‹è¡Œè¿˜æ˜¯ç»“æŸè¡Œï¼Œéœ€å•ç‹¬å®šä¹‰ã€‚
        if dS == dE
            let tempStr = substitute(curStr, '^\s*', "", "")
            let onlyHead = substitute(tempStr, '\s*$', "", "")
        endif

        " å½“é¦–ç¬¦å·ä¸­åŒ…å«ä»¥å°¾ç¬¦å·å­—ç¬¦çš„ç»“å°¾æ—¶éœ€è¦ç‰¹æ®Šå¤„ç†ï¼Œå¦‚ sh ":<<EOF,EOF"ã€‚
        if matchstr(dHeadChar, dTailChar .'$') != "" && dS != dE
            let sameds= escape(dS, "\/'")
            let toTailEnd= matchstr(curStr, '\V\^\s\*'.sameds.'\s\*\$') != ""
        endif

        " @ var bool å¦‚æœæ˜¯ç›¸åŒåŒç¬¦å·ä¸­çš„é¦–æˆ–å°¾ç¬¦å·ç‹¬å ä¸€è¡Œæ—¶ä¸ºçœŸ
        let isSameSymHead = exists("onlyHead") && onlyHead == dS

        " @ var bool å¦‚æœåœ¨é¦–ç¬¦å·ä¸­åŒ…å«ä»¥å°¾ç¬¦å·å­—ç¬¦çš„ç»“å°¾ä¸ºçœŸ
        let isEndSame = exists("toTailEnd") && toTailEnd

        " ä»…å¤„ç†å®Œå…¨ç›¸åŒåŒç¬¦å·çš„é¦–æˆ–å°¾æ˜¯ç‹¬å è¡Œæ—¶çš„èŒƒå›´å–æ¶ˆï¼Œå¦‚ "^\s*'''\s*$"ã€‚
        if isSameSymHead
            if lastNo / 2 > curNo
                let upValue = <SID>SameUpFilter(curNo, dS)

                " å¦‚æœç›¸ç­‰å…ˆä½œä¸ºèŒƒå›´çš„å¼€å§‹è¡Œ
                if upValue == curNo
                    let curNo = upValue
                    let isTrue = <SID>DownFindHead(curNo, dS, dE, lastNo)

                    " å¦‚æœä¸ºçœŸä½œä¸ºèŒƒå›´çš„ç»“æŸè¡Œ
                    if isTrue
                        let endNo = isTrue
                        let l:args = [curNo, endNo, dS, dE, saCursor]
                        let lines = <SID>CompatCancel(l:args)
                    else
                        call <SID>ErrorHandle('Warning', 0)
                        return
                    endif
                else
                    let endNo = curNo
                    let curNo = upValue
                    let l:args = [curNo, endNo, dS, dE, saCursor]
                    let lines = <SID>CompatCancel(l:args)
                endif
            else
                let downValue = <SID>SameDownFilter(curNo, dS, lastNo)

                " å¦‚æœç›¸ç­‰å…ˆä½œä¸ºèŒƒå›´çš„ç»“æŸè¡Œ
                if downValue == curNo
                    let isTrue = <SID>UpFindTail(curNo, dS, dE)

                    " å¦‚æœä¸ºçœŸä½œä¸ºèŒƒå›´çš„å¼€å§‹è¡Œ
                    if isTrue
                        let curNo = isTrue
                        let l:args = [curNo, endNo, dS, dE, saCursor]
                        let lines = <SID>CompatCancel(l:args)
                    else
                        call <SID>ErrorHandle('Warning', 0)
                        return
                    endif
                else
                    let endNo = downValue
                    let l:args = [curNo, endNo, dS, dE, saCursor]
                    let lines = <SID>CompatCancel(l:args)
                endif
            endif
        endif

        " é¦–å’Œå°¾ç¬¦å·éƒ½åœ¨å…‰æ ‡æ‰€åœ¨çš„è¡Œä¸Šæ—¶ä¸ºå–æ¶ˆå•è¡Œæ³¨é‡Š
        "
        " å¯¹å®Œå…¨ç›¸åŒéƒ¨åˆ†å°¾å­—ç¬¦ç›¸åŒçš„åŒç¬¦å·éœ€è¦è¿‡æ»¤ï¼Œå› ä¸ºå®ƒä»¬çš„åŒ¹é…æ€»æ˜¯ç›¸åŒçš„ã€‚
        " !isSameSymHead è¿‡æ»¤å®Œå…¨ç›¸åŒåŒç¬¦å·
        " !isEndSame è¿‡æ»¤éƒ¨åˆ†å°¾å­—ç¬¦ç›¸åŒçš„åŒç¬¦å·
        "
        if dHeadChar == dS && dTailChar == dE && !isSameSymHead && !isEndSame
            let row = <SID>ALineHandle(curNo, dS, dE, s:NOTE_OFF)
        endif

        " é¦–å’Œå°¾ç¬¦å·éƒ½ä¸åœ¨å…‰æ ‡è¡Œä¸Šæ—¶å¯èƒ½æ˜¯å•è¡Œçš„æ³¨é‡Šæˆ–èŒƒå›´çš„å–æ¶ˆæ³¨é‡Š
        "
        " æ”¯æŒç›¸åŒåŒç¬¦å·ï¼ŒåŒæ—¶éšå«çš„å…¼å®¹äº†å¯¹é¦–ç¬¦å·ä¸­åŒ…å«å¹¶ä»¥å°¾ç¬¦å·å­—ç¬¦ç»“å°¾çš„è¡Œ
        "
        if dHeadChar != dS && dTailChar != dE
            if dS != dE
                let isHeadTrue = <SID>UpFindTail(curNo, dS, dE)
                let isTailTrue = <SID>DownFindHead(curNo, dS, dE, lastNo)
            else
                " è¿™åªæœ‰çœŸå€¼
                let l:up = <SID>SameUpFilter(curNo, dS)
                let l:down = <SID>SameDownFilter(curNo, dS, lastNo)

                if l:up != l:down
                    let isHeadTrue = l:up
                    let isTailTrue = l:down
                else
                    let isHeadTrue = 0
                endif
            endif

            if isHeadTrue && isTailTrue && !isPattEach
                let curNo = isHeadTrue
                let endNo = isTailTrue

                let l:args = [curNo, endNo, dS, dE, saCursor]
                let lines = <SID>CompatCancel(l:args)
            else
                " ç¬¬ä¸€åˆ—/åŠæˆå“åˆ—å—æ¨¡å¼çš„å•è¡Œæ³¨é‡Šæ“ä½œ
                if (isPattFirst || isPattSemi) && !isPattEach
                    let isNote = s:NOTE_ON

                    call <SID>CreateDoubleColBlock(curNo,endNo,dS,dE,saCursor)
                    let endNo += 2
                else
                    let row = <SID>ALineHandle(curNo, dS, dE, s:NOTE_ON)
                endif
            endif
        endif

        " å½“å…‰æ ‡åœ¨æ³¨é‡ŠèŒƒå›´ä¸­çš„é¦–è¡Œæ—¶å‘ä¸‹æŸ¥æ‰¾å°¾ç¬¦å·
        "
        " éšå«çš„å…¼å®¹äº†ç›¸åŒåŒç¬¦å·åƒè¿™æ ·çš„è¡Œ "'^\s*'.dS.'\s*\S'"
        " isEndSame å…è®¸é¦–ç¬¦å·ä¸­åŒ…å«å¹¶ä»¥å°¾ç¬¦å·å­—ç¬¦ç»“å°¾çš„å¹¶åªçš„é¦–ç¬¦å·çš„è¡Œ
        " 
        if dHeadChar == dS && dTailChar != dE || isEndSame && !isPattEach
            let isTrue = <SID>DownFindHead(curNo, dS, dE, lastNo)

            if isTrue
                let endNo = isTrue
                let l:args = [curNo, endNo, dS, dE, saCursor]
                let lines = <SID>CompatCancel(l:args)
            else
                call <SID>ErrorHandle('Warning', 0)
                return
            endif
        endif

        " å½“å…‰æ ‡åœ¨æ³¨é‡ŠèŒƒå›´ä¸­çš„æœ«è¡Œæ—¶å‘ä¸ŠæŸ¥æ‰¾é¦–ç¬¦å·
        "
        " éšå«çš„å…¼å®¹äº†åŒç¬¦å·åƒè¿™æ ·çš„è¡Œ "'\S\s*'.dE.'\s*$'"
        "
        if dHeadChar != dS && dTailChar == dE && !isPattEach
            let isTrue = <SID>UpFindTail(curNo, dS, dE)

            if isTrue
                let curNo = isTrue
                let l:args = [curNo, endNo, dS, dE, saCursor]
                let lines = <SID>CompatCancel(l:args)
            else
                call <SID>ErrorHandle('Warning', 0)
                return
            endif
        endif
    endif

    if s:isMsg
        let symchar = dS . s:isDelimiter . dE

        if exists('isNote') && isNote == s:NOTE_ON
            call <SID>ParseMessage(symchar, curNo, endNo, 1, s:presetFlag)

            if exists('originalMessageFlag')
                let s:presetFlag = originalMessageFlag
            endif
        endif

        if exists("lines")
            call <SID>DoubleEcho(symchar,curNo,endNo,"cancel",lines)
        endif

        if exists('row')
            call <SID>DoubleEcho(symchar,curNo,endNo,"aline", row)
        endif
    endif

    call setpos(".", saCursor)
endfunction
"}}}

" s:HighSingleToggle() {{{

" å®šä¹‰é«˜äº®å•ç¬¦å·æ“ä½œå¼€å…³
" @return void
"
function s:HighSingleToggle()
    if exists('b:doubleSymbol')
        call <SID>HighDoubleToggle()
        return
    endif

    " é‡ç”»å±å¹•åæ¶ˆæ¯å¯å›æ˜¾
    redraw
    let saCursor = getpos('.')

    " ä¿®æ­£å¯è§†è¡Œæ—¶å…‰æ ‡çš„åˆ—ä½ç½®
    if visualmode(1) ==# 'V'
        redir => values
            silent marks <
        redir END

        let marksList = split(values, "\n")
        let [mname, mline, mcol; mth] = split(marksList[1])
        let saCursor[2] = mcol + 1
    endif

    if !exists('b:singleSymbol')
        if !exists('b:symbol')
            let b:symbol = <SID>ParseCommentSymbol()
        endif

        let index = stridx(b:symbol[0], s:isDelimiter)

        " ['å•ç¬¦å·']
        if index == -1
            let sS = b:symbol[0]
        else
            " ['é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
            let b:doubleSymbol = [
                \strpart(b:symbol[0], 0, index),
                \strpart(b:symbol[0], index + 1)
            \]

            call <SID>HighDoubleToggle()
            return
        endif
    else
        let sS = b:singleSymbol
    endif

    let curNo = line("'<")
    let endNo = line("'>")
    let limit = endNo + 1

    " è·å–çœŸå®çš„æ“ä½œè¡Œå·
    let curNo = <SID>GetRealCurrentLine(curNo, limit)

    if !curNo
        " å¦‚æœæ“ä½œçš„ç©ºè¡Œæˆ–ç©ºç™½è¡Œæ—¶é€€å‡ºæ— æ„ä¹‰çš„æ“ä½œ
        call setpos('.', saCursor)
        return
    endif

    if !exists('b:singleLen')
        let b:singleLen = strlen(sS)
    endif

    " è°ƒç”¨æ ¸å¿ƒæ“ä½œ
    let result = <SID>SingleExecCore(curNo, limit, sS)

    if s:isMsg
        call <SID>SingleEcho(curNo, sS, result)
    endif

    call setpos('.', saCursor)
endfunction
"}}}

" s:HighDoubleToggle() {{{

" å®šä¹‰é«˜äº®åŒç¬¦å·æ³¨é‡Šå¼€å…³
" @return void
"
function s:HighDoubleToggle()
    if exists('b:singleSymbol')
        call <SID>HighSingleToggle()
        return
    end

    redraw
    let saCursor = getpos(".")

    if visualmode(1) ==# 'V'
        " Vim 7 ä¸æ”¯æŒ execute(), ä½¿ç”¨ redir æ›¿ä»£
        redir => values
            silent marks <
        redir END

        let marksList = split(values, "\n")
        let [mname, mline, mcol; mth] = split(marksList[1])
        let saCursor[2] = mcol + 1
    endif

    if !exists('b:doubleSymbol')
        if !exists('b:symbol')
            let b:symbol = <SID>ParseCommentSymbol()
        endif

        let index = stridx(b:symbol[0], s:isDelimiter)

        if index > -1
            let dS = strpart(b:symbol[0], 0, index)
            let dE = strpart(b:symbol[0], index + 1)
        else
            if index == -1 && len(b:symbol) == 2
                let index = stridx(b:symbol[1], s:isDelimiter)

                let dS = strpart(b:symbol[1], 0, index)
                let dE  = strpart(b:symbol[1], index + 1)
            else
                let b:singleSymbol = b:symbol[0]
                call <SID>HighSingleToggle()
                return
            endif
        endif
    else
        let dS = b:doubleSymbol[0]
        let dE = b:doubleSymbol[1]
    endif

    let curNo = line("'<")
    let endNo = line("'>")

    if !exists('b:dHeadLen')
        let b:dHeadLen = strlen(dS)
        let b:dTailLen = strlen(dE)
    endif

    if !exists('b:prefixLen')
        let b:prefixLen = len(s:isLinePrefix)
    endif

    let curStr = getline(curNo)
    let dHeadChar = <SID>GetHeadChar(curStr, b:dHeadLen)
    let dTailChar = <SID>GetTailChar(curStr, dE)

    if curNo != endNo
        let endStr    = getline(endNo)
        let dTailChar = <SID>GetTailChar(endStr, dE)
    endif

    let isPattFirst = <SID>IsPatternFirstMode()
    let isPattSemi = <SID>IsPatternSemiBlockMode()
    let isPattEach = <SID>IsPatternEachLineMode()

    let originalMessageFlag = s:presetFlag

    if isPattEach
        let curNo = <SID>GetRealCurrentLine(curNo, endNo + 1)
        if !curNo | return | endif

        let pattern = '\V\^\s\*'. escape(dS, '\/') .'\.\*'
        let pattern .= dE .'\s\*\$'

        " @var bool å¦‚æœæ˜¯æ¯ä¸€è¡Œæ¨¡å¼çš„å–æ¶ˆæ ¼å¼ä¸ºçœŸ
        let each = matchstr(getline(curNo), pattern) != ""
    endif

    " ç”±äºå¯è§†æ¨¡å¼æœ‰æŒ‡å®šçš„èŒƒå›´ï¼Œè¿™éšå«äº†å•è¡Œæˆ–å¤šè¡Œçš„å–æ¶ˆæ“ä½œã€‚
    if dHeadChar == dS && dTailChar == dE || exists("each") && each
        let l:args = [curNo, endNo, dS, dE, saCursor]
        let lines = <SID>CompatCancel(l:args)
    else
        " æ³¨é‡Šæ“ä½œä¸æ™®é€šæ¨¡å¼ç¨æœ‰å·®ä¹‰

        " @var bool isMultiLine è¡¨ç¤ºåˆ—å—æ¨¡å¼çš„å¤šè¡Œæ“ä½œ
        " @var bool isPattFirst å•è¡Œæˆ–å¤šè¡Œçš„æ¯ä¸€åˆ—æ¨¡å¼æ“ä½œ
        " @var bool !isPattEach (å–å) é™¤äº†æ¯ä¸€è¡Œæ¨¡å¼

        let isMultiLine = s:isColumnBlockMode &&  curNo != endNo

        if ( isMultiLine || isPattFirst || isPattSemi) && !isPattEach
            call <SID>CreateDoubleColBlock(curNo, endNo, dS, dE, saCursor)
            let endNo += 2

        else
            if isPattEach
                let isNo = <SID>CreateDoubleEachLine(curNo,endNo+1,dS,dE)
                if isNo | let endNo = isNo | endif
            else
                if curNo == endNo && s:isColumnBlockMode
                    let s:presetFlag = s:FLAG_DEFAULT
                endif

                " åˆ—å—çš„å•è¡Œæ“ä½œä½¿ç”¨é»˜è®¤æ¨¡å¼
                let cC = <SID>DoubleEX(curNo, endNo, dS, dE, s:NOTE_ON)
                execute cC[0]
                execute cC[1]
            endif
        endif
    endif

    if s:isMsg
        let symchar = dS . s:isDelimiter . dE

        if exists('lines')
            " @var number å–æ¶ˆæ“ä½œä¸­çœŸå®çš„æœ«è¡Œå·
            let l:last = <SID>DoubleEcho(symchar,curNo,endNo,"cancel",lines)
        else
            call <SID>ParseMessage(symchar, curNo, endNo, 1, s:presetFlag)

            if exists('originalMessageFlag')
                let s:presetFlag = originalMessageFlag
            endif
        endif
    endif

    if s:isColumnBlockMode || isPattFirst || isPattSemi
        execute curNo .'mark <'

        if exists("l:last")
            execute l:last .'mark >'
        else
            execute endNo .'mark >'
        endif
    endif

    call setpos('.', saCursor)
endfunction
"}}}

" s:CreateSingleColBlock(){{{

" å•ç¬¦å·åˆ—å—æ³¨é‡Šæ“ä½œ
" @param number curNo
" @param number limit å¾ªç¯ç•Œé™ï¼Œæœ«è¡Œè¡Œå·åŠ 1
" @param string sS
" @return void
"
function s:CreateSingleColBlock(curNo, limit, sS)
    let getWidth = <SID>GetSmallBlankWidth(a:curNo, a:limit)
    let baseWidth = getWidth[0]

    " ä¸å—ç©ºæ ¼è®¾ç½®çš„å½±å“ g:close_strict_space
    let sS = a:sS . "\x20"

    let incre = a:curNo
    let limit = a:limit

    while incre < limit
        let blankLine = <SID>IsBlankLine(incre)

        " è·³è¿‡ç©ºç™½è¡Œ
        if blankLine
            let incre += 1
            continue
        endif

        let curStr = getline(incre)

        " ä»¥åŸºå‡†ä½ç½®åˆ†å‰²å­—ç¬¦ä¸²
        if baseWidth
            let left  = strpart(curStr, 0, baseWidth)
            let right = strpart(curStr, baseWidth)
        else
            " å¦‚æœæ˜¯ä»¥å¯è§å­—ç¬¦å¼€å¤´çš„é‚£ä¹ˆåŸºå‡†å®½åº¦ä¸º 0
            " @see <SID>GetSmallBlankWidth
            let left  = ""
            let right = curStr
        endif

        let replace = escape(left . sS . right, '\/&')
        execute incre .'s/^.*$/'. replace .'/'

        " å…·æœ‰å®é™…æ“ä½œæœ«è¡Œå·
        let realEndNo = incre

        let incre += 1
    endwhile

    return realEndNo
endfunction
"}}}

" s:SingleExecCore(){{{
"
" å•ç¬¦å·æ“ä½œæ ¸å¿ƒ
" @param number curNo
" @param number limit æœ«è¡Œå·åŠ 1
" @param number sS å•ç¬¦å·
" @return {}
"   "endNo": æœ«è¡Œå·
"   "category": s:NOTE_ON æˆ– s:NOTE_OFF
"   "record": è¢«åè½¬çš„è¡Œæ•°
"
function s:SingleExecCore(curNo, limit, sS)
    let headChar = <SID>GetHeadChar(getline(a:curNo), b:singleLen)

    " @var number æ³¨é‡Šåè½¬çš„è¡Œæ•°
    let reverseRecord = 0

    " @var number å…·æœ‰å®é™…è¢«æ“ä½œä¿®æ”¹çš„æœ«è¡Œå·
    let realEndNo = 0

    let incre = a:curNo

    if headChar == a:sS
        let isNote = s:NOTE_OFF

        while incre < a:limit
            " @var bool blankLine çœŸå€¼è¡¨ç¤ºç©ºç™½è¡Œ
            " @var bool resverComment çœŸå€¼è¡¨ç¤ºå½“å‰è¡Œçš„ä¸‹ä¸€è¡Œå­˜åœ¨æ³¨é‡Šç¬¦å·

            let blankLine = <SID>IsBlankLine(incre)

            if blankLine
                " è¿‡æ»¤æ‰ç©ºç™½è¡Œå¹¶æ‰§è¡Œä¸€ä¸ªç©ºæ“ä½œ
                let cC = <SID>SingleEX(incre, a:sS, s:NOTE_EMPTY)
                let resverComment = cC

                let incre += 1
                continue
            endif

            " æ£€æµ‹åè½¬åŠŸèƒ½æ˜¯å¦å¯ç”¨å¹¶ä¸”åˆ—å—æ¨¡å¼ä¸ä½¿ç”¨åè½¬
            if s:isReverse && !s:isColumnBlockMode
                " æ£€æµ‹æ˜¯å¦éœ€è¦åè½¬
                if exists('resverComment') && !resverComment
                    let cC = <SID>SingleEX(incre, a:sS, s:NOTE_ON)
                    execute cC[0]

                    let realEndNo = incre
                    let resverComment = cC[1]
                    let reverseRecord += 1
                    let incre += 1

                    continue
                endif
            endif

            " åœ¨åè½¬åŠŸèƒ½æ˜¯å…³é—­æ—¶ï¼Œè¿‡æ»¤æ‰æ²¡æœ‰æ³¨é‡Šç¬¦å·çš„è¡Œï¼Œå› ä¸ºå–æ¶ˆæ“ä½œæ˜¯åŒ¹é…ä¸
            " åˆ°æ²¡æœ‰æ³¨é‡Šç¬¦å·çš„è¡Œï¼Œä¼šæŠ¥å‘Šæ‰¾ä¸åˆ°æ¨¡å¼ E486ã€‚ç»™å®ƒä¸€ä¸ªç©ºæ“ä½œå¯ä¿®æ­£
            " å¯¹å¤šè¡Œæ‰§è¡Œå–æ¶ˆæ“ä½œæ—¶ï¼Œå¯¹ä¸è¿ç»­æœ‰ç¬¦å·çš„è¡Œéƒ½å¯ä»¥å–æ¶ˆæ³¨é‡Šã€‚å¯¹æ³¨é‡Šæ“
            " ä½œä¸å­˜åœ¨è¿™æ ·çš„æƒ…å†µï¼Œå› ä¸ºæ³¨é‡Šæ“ä½œæ˜¯å¼ºç¡¬çš„ã€‚
            "
            if exists('resverComment') && !resverComment
                let cC = <SID>SingleEX(incre, a:sS, s:NOTE_EMPTY)

                let resverComment = cC
                let incre += 1

                continue
            endif

            " ç¼ºçœçš„æ“ä½œ
            let cC = <SID>SingleEX(incre, a:sS, s:NOTE_OFF)
            execute cC[0]

            let realEndNo = incre
            let resverComment = cC[1]
            let incre += 1
        endwhile
    else
        let isNote = s:NOTE_ON

        if s:isColumnBlockMode
            let realEndNo = <SID>CreateSingleColBlock(incre, a:limit, a:sS)
        else
            while incre < a:limit
                let bool = <SID>IsBlankLine(incre)

                if bool
                    let cC = <SID>SingleEX(incre, a:sS, s:NOTE_EMPTY)
                    let resverComment = cC
                    let incre += 1
                    continue
                endif

                if s:isReverse
                    if exists('resverComment') && resverComment
                        let cC = <SID>SingleEX(incre, a:sS, s:NOTE_OFF)
                        execute cC[0]

                        let realEndNo = incre
                        let resverComment = cC[1]
                        let reverseRecord += 1
                        let incre += 1

                        continue
                    endif
                endif

                let cC = <SID>SingleEX(incre, a:sS, s:NOTE_ON)
                execute cC[0]

                let realEndNo = incre
                let resverComment = cC[1]
                let incre += 1
            endwhile
        endif
    endif

    return {'endNo':realEndNo, 'category':isNote, 'record':reverseRecord}
endfunction
"}}}

" s:SingleEcho(){{{
"
" å›æ˜¾å•ç¬¦å·æ“ä½œæ¶ˆæ¯
" @see <SID>SingleToggle()
" @see <SID>SingleExecCore()
"
" @param number curNo
" @param string sS
" @param {} info
" @return void
"
function s:SingleEcho(curNo, sS, info)
    let endNo = a:info.endNo

    if s:isReverse && a:info.record
        if a:info.category == s:NOTE_ON
            call <SID>ParseMessage(a:sS, a:curNo, endNo, 1, s:FLAG_REVERSE)
        else
            call <SID>ParseMessage(a:sS, a:curNo, endNo, 0, s:FLAG_REVERSE)
        endif
    else
        if a:info.category == s:NOTE_OFF
            call <SID>ParseMessage(a:sS, a:curNo, endNo, 1, s:presetFlag)
        else
            call <SID>ParseMessage(a:sS, a:curNo, endNo, 0, s:presetFlag)
        endif
    endif
endfunction
"}}}

" s:GetRealCurrentLine(){{{

" è·å–çœŸå®çš„æ“ä½œè¡Œå·ä»…ç”¨äºå•ç¬¦å·æ“ä½œ
" @param number curNo
" @param number limit
" @return number è¿”å›çœŸå®çš„æ“ä½œè¡Œå·ï¼Œå¦‚æœè¿”å› 0 è¡¨ç¤ºæ“ä½œçš„æ˜¯ç©ºè¡Œæˆ–ç©ºç™½è¡Œã€‚
"
function s:GetRealCurrentLine(curNo, limit)
    let curNo = a:curNo
    let bLine = <SID>IsBlankLine(curNo)

    while bLine && curNo < a:limit
        let curNo += 1
        let bLine = <SID>IsBlankLine(curNo)
    endwhile

    if curNo == a:limit
        return 0
    else
        return curNo
    endif
endfunction
"}}}

" s:CreateDoubleColBlock(){{{

" åŒç¬¦å·åˆ—å—æ¨¡å¼çš„æ³¨é‡Šæ“ä½œ
"
" å…³äºæ³¨é‡Šæ ¼å¼: é¦–è¡Œçš„é¦–ç¬¦å·åœ¨æœ€å°ç©ºç™½å®½åº¦ä¹‹åå†™å…¥ï¼Œå…¶å®ƒè¡Œåœ¨æœ€å°ç©ºç™½å®½åº¦åŠ 1å
" çš„ä½ç½®å†™å…¥ã€‚
"
" @param number curNo é¦–è¡Œå·
" @param number endNo æœ«è¡Œå·
" @param string dS é¦–åŒç¬¦å·
" @param string dS å°¾åŒç¬¦å·
" @param list cursor å…‰æ ‡ä½ç½®ä¿¡æ¯
" @return void
"
function s:CreateDoubleColBlock(curNo, endNo, dS, dE, cursor)
    let dS = escape(a:dS, '\/&')
    let dE = escape(a:dE, '\/&')

    let headLineNo = a:curNo
    let tailLineNo = a:endNo

    let valueList = <SID>GetSmallBlankWidth(a:curNo, a:endNo+1)

    let baseWidth = valueList[0]
    let oWidth = baseWidth + 1

    " åˆ›å»ºä¸€ä¸ªç”¨äºé¦–ç¬¦å·çš„ç©ºè¡Œ
    "
    " å…¼å®¹ Ex å‘½ä»¤ "DToggle"
    execute ":". headLineNo

    execute "normal O" | redraw
    let a:cursor[1] += 1
    let tailLineNo += 1

    " åˆ›å»ºä¸€ä¸ªç”¨äºå°¾ç¬¦å·çš„ç©ºè¡Œ
    execute ":". tailLineNo
    execute "normal o" | redraw
    let tailLineNo += 1

    if s:isFirstInsert && <SID>IsPatternFirstMode()
        let s:presetFlag = s:FLAG_FIRST
        call <SID>PadSpace(headLineNo, dS, 0)
        call <SID>PadSpace(tailLineNo, dE, 0)
        return
    endif

    if s:isSemiBlock && <SID>IsPatternSemiBlockMode()
        let s:presetFlag = s:FLAG_SEMI
        call <SID>PadSpace(headLineNo, dS, baseWidth)
        call <SID>PadSpace(tailLineNo, dE, baseWidth)
        return
    endif

    call <SID>PadSpace(headLineNo, dS, baseWidth)
    call <SID>PadSpace(tailLineNo, dE, oWidth)

    let incre = headLineNo + 1
    let limit = tailLineNo

    while incre < limit
        let curStr = getline(incre)
        let curLen = strlen(curStr)

        let blankLine = <SID>IsBlankLine(incre)

        if blankLine
            " å°†ç©ºç™½è¡Œå˜ä¸ºç©ºè¡Œ
            if curLen > 0
                execute incre .'s/^.*$/'

                " ç©ºè¡Œé•¿åº¦ä¸º0ï¼Œå­—ç¬¦ä¸ºç©ºã€‚
                let curStr = ""
                let curLen = 0
            endif

            " å¦‚æœå‰è¾ä¸ºç©ºå¡«å……æ— æ„ä¹‰å¹¶è·³è¿‡ç©ºç™½è¡Œ
            if s:isLinePrefix == ""
                let incre +=1
                continue
            endif

            while curLen < oWidth
                let curStr .= "\x20"
                let curLen = strlen(curStr)
            endwhile
        else
            " éç©ºç™½è¡Œç¼©è¿›ä¸€ä¸ªç©ºæ ¼
            let curStr = "\x20". curStr
        endif

        let left  = strpart(curStr, 0, oWidth)
        let right = strpart(curStr, oWidth)

        " ç¬¦å· & åœ¨æ›¿æ¢ä¸­æœ‰ç‰¹æ®Šæ„ä¹‰ :h &
        let replace = escape(left . s:isLinePrefix . right, '\/&')
        execute incre .'s/^.*$/'. replace

        let incre += 1
    endwhile
endfunction
"}}}

" s:UndoDoubleColBlock{{{

" åŒç¬¦å·å—åˆ—æ¨¡å¼çš„å–æ¶ˆæ“ä½œ
" @param number curNo é¦–è¡Œå·
" @param number endNo æœ«è¡Œå·
" @param string command ä»…ä½œç”¨å†…è¡Œçš„ Ex å­—ç¬¦ä¸²å‘½ä»¤
" @param [] cursor åŸå…‰æ ‡ä½ç½®ä¿¡æ¯
" @return void
"
function s:UndoDoubleColBlock(curNo, endNo, command, cursor)
    let incre = a:curNo + 1

    while incre < a:endNo
        let blankLine = <SID>IsBlankLine(incre)

        " ç©ºç™½è¡Œå¤„ç†ï¼Œè¿™éšå«äº†å¯¹å‰è¾å­—ç¬¦çš„å¤„ç†ã€‚
        if blankLine
            execute incre .'s/^\(\s*\)$/'
        else
            " æ˜¯å¦åªæœ‰å‰è¾å­—ç¬¦çš„è¡Œ
            let iop = <SID>IsOnlyPrefix(incre)

            if iop
                " execute incre . 's/'. iop
                execute incre .'s/^.*$/'
            else
                execute incre . a:command
            end
        endif

        let incre += 1
    endwhile

    " åˆ é™¤é¦–å’Œå°¾è¡Œå¹¶å†™å…¥åˆ° "_ å¯„å­˜å™¨
    execute a:endNo .'g/^.*$/normal "_dd'
    execute a:curNo .'g/^.*$/normal "_dd'

    call <SID>RestoreCursor(a:curNo, a:endNo, a:cursor)
endfunction
"}}}

" s:CreateDoubleEachLine(){{{
"
" æ¯ä¸€è¡Œæ¨¡å¼çš„åŒç¬¦å·åˆ›å»º
" @param number curNo
" @param number limit
" @param string dS
" @param string dE
" @return void
"
function s:CreateDoubleEachLine(curNo, limit, dS, dE)
    let cC = <SID>DoubleEX(a:curNo, a:limit-1, a:dS, a:dE, s:NOTE_ON)

    " é¦–å’Œå°¾ç¬¦å·åœ¨ <SID>DoubleEX ä¸­è½¬ä¹‰
    let dS = escape(a:dS, '\/')
    let dE = escape(a:dE, '\/')

    let realEndNo = 0

    let incre = a:curNo

    while incre < a:limit
        let blankLine = <SID>IsBlankLine(incre)

        if blankLine
            let incre += 1
            continue
        endif

        let pattern = '\V\^\s\*'. dS .'\.\*'. dE .'\s\*\$'
        let correct = matchstr(getline(incre), pattern) == ""

        if correct
            execute incre cC[0]
            execute incre cC[1]
            let realEndNo = incre
        endif

        let incre += 1
    endwhile

    let s:presetFlag = s:FLAG_EACH
    return realEndNo
endfunction
"}}}

" s:UndoDoubleEachLine(){{{
"
" æ¯ä¸€è¡Œæ¨¡å¼çš„åŒç¬¦å·å–æ¶ˆ
" @see <SID>CreateDoubleEachLine()
"
function s:UndoDoubleEachLine(curNo, limit, dS, dE)
    let cC = <SID>DoubleEX(a:curNo, a:limit-1, a:dS, a:dE, s:NOTE_OFF)

    let dS = escape(a:dS, '\/')
    let dE = escape(a:dE, '\/')

    let realEndNo = 0

    let incre = a:curNo

    while incre < a:limit
        let blankLine = <SID>IsBlankLine(incre)

        if blankLine
            let incre += 1
            continue
        endif

        let pattern = '\V\^\s\*'. dS .'\.\*'. dE .'\s\*\$'
        let correct = matchstr(getline(incre), pattern) != ""

        if correct
            execute incre cC[0]
            execute incre cC[1]
            let realEndNo = incre
        endif

        let incre += 1
    endwhile

    return realEndNo
endfunction
"}}}

" s:CompatCancel(){{{

" æ³¨é‡Šæ¨¡å¼å…¼å®¹çš„å¤šè¡Œå–æ¶ˆæ³¨é‡Š
" @param [] input 0é¦–è¡Œå· 1æœ«è¡Œå· 2é¦–ç¬¦å· 3æœ«ç¬¦å· 4å…‰æ ‡ä¿¡æ¯çš„åˆ—è¡¨
" @return number|string|list|null
"   number  åœ¨åˆ—å—æ¨¡å¼æˆ–ç¬¬ä¸€åˆ—æ¨¡å¼
"   string  åœ¨åˆ—å—æ¨¡å¼å–æ¶ˆé»˜è®¤æ¨¡å¼
"   list    [number real_number, string original_message_flag]
"           åœ¨åˆ—å—æ¨¡å¼å–æ¶ˆç¬¬ä¸€åˆ—æ¨¡å¼
"           åœ¨é»˜è®¤æ¨¡å¼å–æ¶ˆåˆ—å—æ ¼å¼
"           åœ¨é»˜è®¤æ¨¡å¼å–æ¶ˆç¬¬ä¸€åˆ—æ¨¡å¼
"   null    åœ¨é»˜è®¤æ¨¡å¼å–æ¶ˆé»˜è®¤æ¨¡å¼çš„è¿”å›ç©º

function s:CompatCancel(input)
    let first = a:input[0]
    let l:last = a:input[1]
    let dS  = a:input[2]
    let dE  = a:input[3]
    let cursor = a:input[4]

    let temp = s:presetFlag

    let comm = <SID>DoubleEX(first, l:last, dS, dE, s:NOTE_OFF)

    let isFormat = <SID>IsStrictFormat(first, l:last, dS, dE)

    let isPattFirst = <SID>IsPatternFirstMode()
    let isPattEach = <SID>IsPatternEachLineMode()
    let isPattSemi = <SID>IsPatternSemiBlockMode()

    if isPattFirst || isFormat[0] || isPattSemi
        let realEndNo = l:last - 2
    endif

    if s:isColumnBlockMode
        if isFormat[0]
            call <SID>UndoDoubleColBlock(first, l:last, comm, cursor)
            return realEndNo
        else
            let s:presetFlag = s:FLAG_DEFAULT

            let s:isColumnBlockMode = 0
            let dEx = <SID>DoubleEX(first, l:last, dS, dE, s:NOTE_OFF)
            let s:isColumnBlockMode = 1

            if isPattEach
                let s:presetFlag = s:FLAG_EACH
                let endNo = <SID>UndoDoubleEachLine(first, l:last+1, dS, dE)
                return [endNo, temp]
            endif

            " é»˜è®¤/ç¬¬ä¸€åˆ—/åŠæˆå“
            execute dEx[0]
            execute dEx[1]

            if isPattFirst
                let s:presetFlag = s:FLAG_FIRST
                call <SID>RestoreCursor(first, l:last, cursor)
                return [realEndNo, temp]
            endif

            if isPattSemi
                let s:presetFlag = s:FLAG_SEMI

                call <SID>RestoreCursor(first, l:last, cursor)
                return [realEndNo, temp]
            endif

            return temp
        endif
    else
        if isFormat[0]
            let s:presetFlag = s:FLAG_BLOCK

            let s:isColumnBlockMode = 1
            let cEx = <SID>DoubleEX(first, l:last, dS, dE, s:NOTE_OFF)
            let s:isColumnBlockMode = 0

            call <SID>UndoDoubleColBlock(first, l:last, cEx, cursor)
            return [realEndNo, temp]
        else
            if isPattEach
                let s:presetFlag = s:FLAG_EACH
                let endNo = <SID>UndoDoubleEachLine(first, l:last+1, dS, dE)
                return [endNo, temp]
            endif

            execute comm[0]
            execute comm[1]

            if isPattFirst
                let s:presetFlag = s:FLAG_FIRST

                call <SID>RestoreCursor(first, l:last, cursor)
                return [realEndNo, temp]
            endif

            if isPattSemi
                let s:presetFlag = s:FLAG_SEMI

                call <SID>RestoreCursor(first, l:last, cursor)
                return [realEndNo, temp]
            endif

            return v:null
        endif
    endif
endfunction
"}}}

" s:ALineHandle(){{{

" åŒç¬¦å·å•è¡Œæ³¨é‡Šæˆ–å–æ¶ˆæ³¨é‡Š
" @see <SID>DoubleToggle()
" @param number curNo
" @param string dS
" @param string dE
" @param number type s:NOTE_ON | s:NOTE_OFF
" @return number|list [number type, string message_flag]
"
function s:ALineHandle(curNo, dS, dE, type)
    if s:isColumnBlockMode
        let temp = s:presetFlag
        let s:presetFlag = s:FLAG_DEFAULT
        " å¤ä½
        let s:isColumnBlockMode = 0
    endif

    " è°ƒç”¨é»˜è®¤æ¨¡å¼çš„ Ex å­—ç¬¦ä¸²å‘½ä»¤
    let eX = <SID>DoubleEX(a:curNo, a:curNo, a:dS, a:dE, a:type)
    execute eX[0]
    execute eX[1]

    if exists('temp')
        let s:isColumnBlockMode = 1
    endif

    " æ¯ä¸€è¡Œæ¨¡å¼å•è¡Œæ“ä½œæ¶ˆæ¯æ ‡è®°
    if <SID>IsPatternEachLineMode()
        let temp = s:presetFlag
        let s:presetFlag = s:FLAG_EACH
    endif

    if exists('temp')
        return [a:type, temp]
    else
        return a:type
    endif

endfunction
"}}}

" s:DoubleEcho(){{{
"
" å›æ˜¾åŒç¬¦å·æ“ä½œæ¶ˆæ¯
"
" ç”±äºä¸åŒæ¨¡å¼çš„å½±å“ï¼ŒåŠå–æ¶ˆçš„å…¼å®¹æ“ä½œï¼Œå¯¼è‡´è¿”å›æ¶ˆæ¯ä¸æ•´é½ã€‚éœ€è¦åˆ†åˆ«å¤„ç†ã€‚
"
" @see <SID>CompatCancel()
" @see <SID>ALineHandle()
" @see <SID>DoubleToggle()
" @see <SID>HighDoubleToggle()
" @see <SID>ParseMessage()
"
" @param string symbol
" @param number curNo
" @param number endNo
" @param string category
"   "cancel" å¤šè¡Œå–æ¶ˆ
"   "aline"  å•è¡Œæ³¨é‡Šæˆ–å–æ¶ˆæ³¨é‡Š
" @param mixed info æ ¹æ® category çš„ä¸åŒè¿”å›ä¸åŒçš„å€¼
" @return void
"
function s:DoubleEcho(symbol,curNo,endNo,category,info)
    let l:endNo = a:endNo

    if a:category == 'cancel'
        if type(a:info) == type([])
            " v:t_list
            let l:endNo = a:info[0]
            let originalMessageFlag = a:info[1]
        elseif type(a:info) == type(0)
            " v:t_number
            let l:endNo = a:info
        elseif type(a:info) == type("")
            " v:t_string
            let originalMessageFlag = a:info
        elseif type(a:info) == type(v:none)
            " v:t_none
        endif

        call <SID>ParseMessage(a:symbol, a:curNo, l:endNo, 0, s:presetFlag)
    endif

    if a:category == 'aline'
        if type(a:info) == type([])
            let opt = a:info[0]
            let originalMessageFlag = a:info[1]
        else
            let opt = a:info
        endif

        if opt == s:NOTE_ON
            call <SID>ParseMessage(a:symbol, a:curNo, a:endNo, 1,s:presetFlag)
        endif

        if opt == s:NOTE_OFF
            call <SID>ParseMessage(a:symbol, a:curNo, a:endNo, 0,s:presetFlag)
        endif
    endif

    " æ¢å¤åŸæ¨¡å¼æ¶ˆæ¯æ ‡è®°
    if exists("originalMessageFlag")
        let s:presetFlag = originalMessageFlag
    endif

    return l:endNo
endfunction
"}}}

" s:EmptyNote(){{{
" 
" å¸¦æ‘˜è¦çš„ç©ºæ³¨é‡Š
" @return string è¿”å›ç©ºå­—ç¬¦ "\x0"
"
function s:EmptyNote(sLine, eLine)
    stopinsert
    let saCursor = getpos(".")

    let sym = <SID>ParseCommentSymbol()

    let index = stridx(sym[0], s:isDelimiter)

    " ['é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
    if index > -1
        let dS = strpart(sym[0], 0, index)
        let dE = strpart(sym[0], index + 1)
    else
        " ['å•ç¬¦å·', 'é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
        if index == -1 && len(sym) == 2
            let index = stridx(sym[1], s:isDelimiter)
            let dS = strpart(sym[1], 0, index)
            let dE = strpart(sym[1], index + 1)
        else
            " å¯¹ä¸å¯¹æ”¯æŒåŒç¬¦å·æ–‡ä»¶åœæ­¢æ“ä½œ
            return
        endif
    endif

    let dS = escape(dS, '\/&')
    let dE = escape(dE, '\/&')

    let curNo = line(".")
    let temp = curNo

    let amount = str2nr(nr2char(getchar()))
    if amount < 3 | let amount = 3 | endif

    let headPos = strlen(matchstr(getline(curNo), '^\s*'))
    let otherPos = headPos + 1

    let sumLen = len(s:isSummary)

    " é¦–ç¬¦å·è¡Œ
    call <SID>PadSpace(temp, dS, headPos)

    let incre = 0

    while incre <= amount
        execute "normal o\<Esc>"
        let temp += 1

        let scaler = 0
        let str = ""

        while scaler < otherPos
            let str .= "\x20"
            let scaler = strlen(str)
        endwhile

        if sumLen && incre
            execute temp .'s/^.*$/'. str ."*\x20". s:isSummary[incre-1]
            let sumLen -= 1
        else
            execute temp .'s/^.*$/'. str ."*\x20"
        endif

        let incre += 1
    endwhile

    " æœ«ç¬¦å·è¡Œ
    call <SID>PadSpace(temp, dE, otherPos)

    let saCursor[1] = curNo + 1
    call setpos(".", saCursor)

    startinsert!

    " è¿”å›ç©ºæ˜¯æœ‰å¿…è¦çš„ï¼Œå¦åˆ™é»˜è®¤è¿”å›å€¼ 0 åœ¨æ’å…¥æ¨¡å¼å…‰æ ‡ä¹‹å‰ã€‚
    return "\x0"
endfunction
"}}}

" s:ParseCommentSymbol() {{{
"
" è§£ææ³¨é‡Šç¬¦å·
" @return [] è¿”å›åŒ…å«äº†æ³¨é‡Šç¬¦å·çš„åˆ—è¡¨,è¯¥åˆ—è¡¨é•¿åº¦ä¸æ˜¯1å°±æ˜¯2
"
function s:ParseCommentSymbol()
    if !exists('g:add_comment_symbol')
        return [s:isDefinedS, s:isDefinedD]
    endif

    " call <SID>IsValueCorrect()

    let ft  = &filetype
    let fn  = expand('%')
    let ext = expand('%:e')

    " è§£ææ ¼å¼
    "
    " ['ft:php,javascript,css', '//', '/*,*']
    " [['ft:php,javascript,css', '//', '/*,*'], ...]

    let tempCell = []

    for item in g:add_comment_symbol
        if type(item) != type([])
            " @var string item ä¸€ç»´åˆ—è¡¨å­—ç¬¦ä¸²é¡¹æ·»åŠ ä¸ºæ–°åˆ—è¡¨
            call add(tempCell, item)
            continue
        endif

        if !empty(tempCell)
            let item = tempCell
        endif

        " @var list item
        " @var list temp
        let temp = split(item[0], ':')
        let headType = temp[0]

        " @var list
        let headCell = split(temp[1], s:isDelimiter)
        let headLen  = len(headCell)

        " @var list æ³¨é‡Šç¬¦å·åˆ—è¡¨ï¼Œæœªè®¾ç½®ç¬¦å·æ—¶ä¸ºç©ºåˆ—è¡¨
        let symCell = item[1:]

        if empty(symCell)
            let result = [s:isDefinedS, s:isDefinedD]
        else
            let symLen = len(symCell)

            if symLen == 1
                " ['é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
                " ['å•ç¬¦å·']
                let result = [symCell[0]]
            else
                " ['å•ç¬¦å·', 'é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']
                " ['é¦–åŒç¬¦å·,å°¾åŒç¬¦å·', 'å•ç¬¦å·']

                let l:index = stridx(symCell[0], s:isDelimiter)

                " è°ƒæ•´é¡ºåºå ['å•ç¬¦å·', 'é¦–åŒç¬¦å·,å°¾åŒç¬¦å·']ï¼Œè¿™æ˜¯æœ‰å¿…è¦çš„ï¼Œåœ¨
                " åˆ†æè¯¥å‡½æ•°è¿”å›å€¼æ—¶ï¼Œä¸å¿…è€ƒè™‘é¡ºåºã€‚
                if l:index == -1
                     let result = [symCell[0], symCell[1]]
                else
                     let result = [symCell[1], symCell[0]]
                endif
            endif
        endif

        " @var number è‡ªå¢çš„åˆå§‹å€¼0
        let incre = 0

        if headType == 'ft'
            while incre < headLen
                if ft == headCell[incre]
                    return result
                endif

                let incre += 1
            endwhile
        elseif headType == 'fn'
            while incre < headLen
                if fn == headCell[incre]
                    return result
                endif

                let incre += 1
            endwhile
        elseif headType == 'ext'
            while incre < headLen
                if ext == headCell[incre]
                    return result
                endif
                let incre += 1
            endwhile
        endif
    endfor

    " å¦‚æœæ£€æµ‹ä¸åˆ° g:add_comment_symbol ä¸­çš„å…³è”ä½¿ç”¨é»˜è®¤çš„æ³¨é‡Šç¬¦å·
    return [s:isDefinedS, s:isDefinedD]
endfunction
"}}}

" s:ParseMessage() {{{

" è§£æå›æ˜¾æ¶ˆæ¯
" @see s:isCustom
" @see s:FLAG_DEFAULT
"
" @param string symbol some  some[0] å’Œè¡Œå·ä¿¡æ¯ some[1]
" @param number curNo some  some[0] å’Œè¡Œå·ä¿¡æ¯ some[1]
" @param number endNo some  some[0] å’Œè¡Œå·ä¿¡æ¯ some[1]
" @param int item   0å–æ¶ˆæ³¨é‡Šæ¶ˆæ¯ 1æ³¨é‡Šæ¶ˆæ¯
" @param string flag
" @return void
"
function s:ParseMessage(symbol, curNo, endNo, item, flag)
    " Annotation symbol
    let temp = a:symbol . s:A_BLANK_SPACE

    " Message flag
    if a:flag == s:FLAG_FIRST
        let temp .= s:FLAG_FIRST

    elseif a:flag == s:FLAG_DEFAULT
        let temp .= s:FLAG_DEFAULT

    elseif a:flag == s:FLAG_BLOCK
        let temp .= s:FLAG_BLOCK

    elseif a:flag == s:FLAG_REVERSE
        let temp .= s:FLAG_REVERSE

    elseif a:flag == s:FLAG_SEMI
        let temp .= s:FLAG_SEMI

    elseif a:flag == s:FLAG_EACH
        let temp .= s:FLAG_EACH
    endif

    " Single line
    if a:curNo == a:endNo
        let temp .=  s:A_BLANK_SPACE . a:curNo
    else
        " Multi line
        let temp .= s:A_BLANK_SPACE
        let temp .= a:curNo . s:isDelimiter . a:endNo
    endif

    " Output message
    echohl Tag | echo '['. temp .'] '. s:isCustom[a:item] | echohl None
endfunction
"}}}

" s:SingleEX() {{{
"
" å•ç¬¦å· Ex å‘½ä»¤
" æ‹¼è£…å•ç¬¦å·æ“ä½œçš„ Ex å­—ç¬¦ä¸²å‘½ä»¤ã€‚å•ç¬¦å·åˆ—å—çš„æ³¨é‡Šæ“ä½œä¸ç»è¿‡æ­¤å‡½æ•°ã€‚
"
" @param number curNo
" @param string sS å•ç¬¦å·
" @param number type æ“ä½œç±»å‹: s:NOTE_OFF | s:NOTE_ON | s:NOTE_EMPTY
" @return bool | [string Ex, bool flag ]
"     bool è¡¨ç¤ºå½“å‰è¡Œçš„ä¸‹ä¸€è¡Œå­˜åœ¨å•ç¬¦å·ä¸ºçœŸï¼Œåä¹‹ä¸ºå‡ã€‚
"     Ex è¿”å›åˆ—è¡¨åŒ…å«äº†å­—ç¬¦ä¸²å½¢å¼çš„ Ex å‘½ä»¤
"     flag è¡¨ç¤ºå½“å‰è¡Œçš„ä¸‹ä¸€è¡Œå­˜åœ¨å•ç¬¦å·ä¸ºçœŸï¼Œåä¹‹ä¸ºå‡ã€‚
"
function s:SingleEX(curNo, sS, type)
    let sS = escape(a:sS, '/\')
    let strict = sS . s:isBlank

    let currStr = getline(a:curNo)
    let nextStr = getline(a:curNo + 1)

    " æ£€æµ‹æ“ä½œè¡Œå•ç¬¦å·ä¹‹åçš„ç©ºæ ¼æ•°é‡ã€‚
    " å½“ç©ºæ ¼çš„æ•°é‡åˆ†åˆ«ä¸º 0 ä¸ªæˆ– 1 ä¸ªå’Œè‡³å°‘ 2 ä¸ªæ—¶ä½¿ç”¨ä¸åŒçš„åŒ¹é…æ›¿æ¢ã€‚
    " è¿™æ ·åœ¨å–æ¶ˆæ³¨é‡Šæ—¶æ›´çµæ´»å¹¶å¯¹è‡³å°‘2ä¸ªçš„ç©ºæ ¼æ“ä½œæ—¶ä¹Ÿä¸å½±å“æ­£å¸¸çš„ç¼©è¿›ã€‚
    let currGtLt2Space = matchstr(currStr, '\V\^\(\s\*\)'. sS .'\s\{2,}')

    " @var string patCo_1
    " @var string patCo_2
    " @var bool patCo å½“å‰è¡Œçš„ä¸‹ä¸€è¡Œå­˜åœ¨å•ç¬¦å·ä¸ºçœŸï¼Œå°†ç”¨äºåè½¬çš„æ ‡è¯†ã€‚

    let patCo_1 = matchstr(nextStr, '\V\^'. sS)
    let patCo_2 = matchstr(nextStr, '\V\^\s\+'. sS)
    let patCo   = patCo_1 != "" || patCo_2 != ""

    if a:type == s:NOTE_EMPTY
        return patCo
    endif

    if a:type == s:NOTE_OFF
        " åˆ—å—å–æ¶ˆæ“ä½œä¸å—ç©ºæ ¼è®¾ç½®çš„å½±å“
        if s:isColumnBlockMode
            let strict = sS . "\x20"
        endif

        if currGtLt2Space != ""
            " å½“ä¸¤ä¸ªä»¥ä¸Šç©ºæ ¼(åŒ…æ‹¬ä¸¤ä¸ª)æ—¶å¯¹ç©ºæ ¼ä¸¥æ ¼
            return [
                \ a:curNo .'s/\V\^\(\s\*\)'. strict .'\(\.\*\)\$/\1\2/',
                \ patCo]
        else
            " å½“ç©ºæ ¼ä¸º 0 ä¸ªæˆ– 1 ä¸ªæ—¶ç©ºæ ¼ä¸ä¸¥æ ¼
            return [
                \ a:curNo .'s/\V\^\(\s\*\)'. sS .'\s\?\(\.\*\)\$/\1\2/',
                \ patCo]
        endif
    endif

    if a:type == s:NOTE_ON
        let strict = escape(strict, '&')
        return [a:curNo.'s/\V\^\(\s\*\)\(\.\*\)\$/\1'. strict .'\2/', patCo]
    endif
endfunction
"}}}

" s:DoubleEX() {{{
"
" åŒç¬¦å·æ“ä½œå‘½ä»¤çš„æ‹¼è£…
" @param number curNo
" @param number endNo   å¦‚æœå’Œ curNo ç›¸åŒå°†ä½œç”¨äºå•è¡Œ
" @param string dS      é¦–åŒç¬¦å·
" @param string dE      å°¾åŒç¬¦å·
" @param number type    æ“ä½œç±»å‹: s:NOTE_OFF æˆ– s:NOTE_ON
" @return string | list
"   string  ä»…åˆ—å—æ¨¡å¼å†…è¡Œçš„ Ex å­—ç¬¦ä¸²å‘½ä»¤
"   list    [string tail_command, string head_command]
"               tail_command å°¾ç¬¦å·è¡Œå­—ç¬¦ä¸²å½¢å¼çš„ Ex å‘½ä»¤
"               head_command é¦–ç¬¦å·è¡Œå­—ç¬¦ä¸²å½¢å¼çš„ Ex å‘½ä»¤
"
function s:DoubleEX(curNo, endNo, dS, dE, type)
    let isPattEach = <SID>IsPatternEachLineMode()

    " å–æ¶ˆæ“ä½œ: æ‰€æœ‰æ¨¡å¼ï¼Œä¸åŒ…æ‹¬åˆ—å—çš„é¦–å’Œå°¾è¡Œã€‚
    if a:type == s:NOTE_OFF
        let dS = escape(a:dS, '/\')
        let dE = escape(a:dE, '/\')

        if isPattEach
            let tail = 's/\V\s\?'. dE .'\s\*\$'
            let head = 's/\V\^\(\s\*\)'. dS .'\s\?/\1'
            return [tail, head]
        endif

        if s:isColumnBlockMode
            " @see <SID>UndoDoubleColBlock()
            let prefix = escape(s:isLinePrefix, '\/')
            return 's/\V\^\(\s\*\)\s\{1}'. prefix .'/\1/'
        else
            let isHeadEnd = <SID>IsHeadSymEnd(a:curNo, a:dS, 0)
            let isTailEnd = <SID>IsTailSymEnd(a:endNo, a:dE, 0)

            let isPattFirst = <SID>IsPatternFirstMode()
            let isPattSemi = <SID>IsPatternSemiBlockMode()

            if isHeadEnd
                if isPattFirst || isPattSemi
                    let head = a:curNo .'g/^.*$/normal "_dd'
                else
                    let head = a:curNo .'s/^.*$/'
                endif
            else
                let head = a:curNo .'s/\V\^\(\s\*\)'. dS .'\s\?/\1/'
            endif

            if isTailEnd
                if isPattFirst || isPattSemi
                    let tail = a:endNo .'g/^.*$/normal "_dd'
                else
                    let tail = a:endNo .'s/^.*$/'
                endif
            else
                let tail = a:endNo .'s/\V\s\?' . dE .'\s\*\$/'
            endif

            return [tail, head]
        endif
    endif

    " æ³¨é‡Šæ“ä½œ: ä»…é»˜è®¤æ¨¡å¼å’Œæ¯ä¸€è¡Œæ¨¡å¼
    if a:type == s:NOTE_ON
        let dS = escape(a:dS, '/\#&')
        let dE = escape(a:dE, '/\#&')

        if isPattEach
            let head = 's#\v^(\s*)#\1'. dS . s:isBlank
            let tail = 's#\v(.*)$#\1'. s:isBlank . dE
            return [head, tail]
        endif

        let smallWidth = <SID>GetSmallBlankWidth(a:curNo, a:endNo+1)
        let insertPosition = smallWidth[0]

        let isHeadBlank = <SID>IsBlankLine(a:curNo)

        if isHeadBlank
            call <SID>PadSpace(a:curNo, dS, insertPosition)
            let head = ""
        else
            let head = a:curNo. 's#\v^(\s*)(.*)$#\1'. dS . s:isBlank .'\2#'
        endif

        " @var bool æ‰§è¡Œä½ç½®éšå«äº†å•è¡Œæ“ä½œæ—¶é¦–ç¬¦å·ä¸ä¼šè¢«å°¾ç¬¦å·è¦†ç›–
        let isTailBlank = <SID>IsBlankLine(a:endNo)

        if isTailBlank
            call <SID>PadSpace(a:endNo, dE, insertPosition)
            let tail = ""
        else
            let tail = a:endNo. 's#\v^(.*)$#\1'. s:isBlank . dE .'#'
        endif

        return [head, tail]
    endif
endfunction
"}}}

" s:UpFindTail() {{{
"
" å‘ä¸ŠæŸ¥æ‰¾åªæœ‰ä¸¤ç§æƒ…å†µ: åœ¨å°¾ç¬¦å·çš„è¡Œå‘ä¸ŠæŸ¥æ‰¾;åœ¨é¦–å’Œå°¾ç¬¦å·éƒ½ä¸å­˜åœ¨çš„è¡Œå‘ä¸Šæ‰¾ã€‚
" @param number lineNo  å½“å‰è¡Œå·
" @param string needleS é¦–ç¬¦å·
" @param string needleE å°¾ç¬¦å·
" @return number 0|1
"
function s:UpFindTail(lineNo, needleS, needleE)
    if a:lineNo == 1
        return 0
    endif

    let prevNo = a:lineNo - 1

    while prevNo
        let str = getline(prevNo)

        if a:needleS != a:needleE
            let ndS = <SID>GetHeadChar(str, b:dHeadLen)
            let ndE = <SID>GetTailChar(str, a:needleE)

            " ä¼˜å…ˆæ‰¾å°¾ç¬¦å·ï¼Œé˜²æ­¢ä¸€è¡Œçš„é¦–å’Œå°¾ç¬¦å·åŒæ—¶å­˜åœ¨ã€‚
            if ndE == a:needleE
                " è¿‡æ»¤å°¾å­—ç¬¦ï¼Œå¦‚ sh çš„åŒç¬¦å· ":<<EOF,EOF"ï¼Œæœ‰ç›¸åŒçš„ EOF ç»“å°¾ã€‚
                if ndS != a:needleS
                    return 0
                end
            endif

            if ndS == a:needleS
                return prevNo
            endif
        else
            " å¯¹äºç›¸åŒçš„åŒç¬¦å·å·²ç¡®å®šçš„å°¾ç¬¦å·å‘ä¸ŠæŸ¥æ‰¾
            let dS = escape(a:needleS, '\/')
            " char '''
            let ndS = matchstr(str, '\S\s*'. dS .'\s*$') != ""
            " '''
            let onlyS = matchstr(str, '^\s*'. dS .'\s*$') != ""
            " ''' char
            let charE = matchstr(str, '^\s*'. dS .'\s*\S') != ""

            if ndS
                return 0
            endif

            if onlyS || charE
                return prevNo
            endif
        endif

        let prevNo -= 1
    endwhile

    return 0
endfunction
"}}}

" s:DownFindHead() {{{
"
" å‘ä¸‹æ‰¾åªæœ‰ä¸¤ç§æƒ…å†µ: åœ¨é¦–ç¬¦å·çš„è¡Œå‘ä¸‹æŸ¥æ‰¾; åœ¨é¦–å’Œå°¾ç¬¦å·éƒ½ä¸å­˜åœ¨çš„è¡Œå‘ä¸‹æŸ¥æ‰¾ã€‚
" @param number lineNo æ“ä½œè¡Œå·
" @param string needleS é¦–åŒç¬¦å·
" @param string needleE å°¾åŒç¬¦å·
" @param number lastNo å½“å‰æ–‡ä»¶çš„æœ€å¤§è¡Œå·
" @return number è¿”å› 0 è¡¨ç¤ºå½“å‰è¡Œä¸ºå•è¡Œæ³¨é‡Šæ“ä½œï¼Œé 0 å€¼å–æ¶ˆå¤šè¡Œæ³¨é‡Šçš„æ“ä½œã€‚
"
function s:DownFindHead(lineNo, needleS, needleE, lastNo)
    if a:lineNo == a:lastNo
        return 0
    endif

    let nextNo = a:lineNo + 1

    while nextNo
        let str = getline(nextNo)

        if a:needleS != a:needleE
            let ndS = <SID>GetHeadChar(str, b:dHeadLen)
            let ndE = <SID>GetTailChar(str, a:needleE)

            if ndS == a:needleS
                return 0
            endif

            if ndE == a:needleE
                return nextNo
            endif
        else
            let dS = escape(a:needleS, '\/')
            let ndS = matchstr(str, '^\s*'. dS . '\s*\S') != ""
            let onlyS = matchstr(str, '^\s*'. dS .'\s*$') != ""
            let charE = matchstr(str, '\S\s*'. dS .'\s*$') != ""

            if ndS
                return 0
            endif

            if onlyS || charE
                return nextNo
            endif
        endif

        let nextNo += 1

        if nextNo > a:lastNo
            return 0
        endif
    endwhile
endfunction
"}}}

" s:SameUpFilter(){{{
"
" @see <SID>SameDownFilter()
" @param number curNo
" @param string dS
" @return number
"   å¦‚æœè¿”å›çš„è¡Œå·æ˜¯ a:curNoï¼Œå°†å½“å‰è¡Œå·ä½œä¸ºèŒƒå›´çš„å¼€å§‹ï¼Œå¹¶éšå«äº†å‘ä¸Šåªæ‰¾åˆ°èŒƒå›´
"   çš„å¼€å§‹è¡Œã€‚
"
"   å¦‚æœè¿”å›çš„ä¸æ˜¯ a:curNoï¼Œå°†å‘ä¸ŠæŸ¥æ‰¾çš„ç¬¬ä¸€ä¸ªå¸¦æœ‰ç¬¦å·çš„è¡Œå·ä½œä¸ºèŒƒå›´çš„å¼€å§‹ï¼Œå¹¶
"   éšå«äº†å‘ä¸Šå·²ç»æŸ¥æ‰¾åˆ°èŒƒå›´çš„å¼€å§‹è¡Œå’Œç»“æŸè¡Œã€‚
"
function s:SameUpFilter(curNo, dS)
    if a:curNo == 1
        return a:curNo
    endif

    let temp = []
    let scaler = 0

    let incre = a:curNo - 1

    while incre
        let curStr = getline(incre)

        if matchstr(curStr, '\S\s*'. a:dS .'\s*$') != ""
            break
        endif

        if matchstr(curStr, '^\s*'.a:dS .'\s*\S') != ""
            if empty(temp)
                return incre
            endif

            let scaler += 1
            break
        endif

        if matchstr(curStr, '^\s*'. a:dS) != ""
            call add(temp, incre)
            let scaler += 1
        endif

        let incre -= 1
    endwhile

    if scaler % 2 == 0
        return a:curNo
    else
        return temp[0]
    endif
endfunction
"}}}

"s:SameDownFilter(){{{
"
" ç›¸åŒåŒç¬¦å·å‘ä¸‹æŸ¥æ‰¾è¿‡æ»¤
" @param number curNo æ“ä½œè¡Œå·
" @param string dS ç›¸åŒåŒç¬¦å·ä¸­çš„é¦–ç¬¦å·ï¼Œå¦‚ python çš„ "'''"
" @param number lastNo å½“å‰æ–‡ä»¶çš„æœ€å¤§è¡Œå·
" @return number è¿”å›è¡Œå·
"   å¦‚æœè¿”å›çš„è¡Œå·æ˜¯ a:curNoï¼Œå°†å½“å‰è¡Œå·ä½œä¸ºèŒƒå›´çš„ç»“æŸï¼Œå¹¶éšå«äº†å‘ä¸‹åªæ‰¾åˆ°èŒƒå›´
"   çš„ç»“æŸè¡Œã€‚
"
"   å¦‚æœè¿”å›çš„ä¸æ˜¯ a:curNoï¼Œå°†å‘ä¸‹æŸ¥æ‰¾çš„ç¬¬ä¸€ä¸ªå¸¦æœ‰ç¬¦å·çš„è¡Œå·ä½œä¸ºèŒƒå›´çš„ç»“æŸè¡Œï¼Œ
"   å¹¶éšå«äº†å‘ä¸‹å·²ç»æŸ¥æ‰¾åˆ°èŒƒå›´çš„å¼€å§‹è¡Œå’Œç»“æŸè¡Œã€‚
"
function s:SameDownFilter(curNo, dS, lastNo)
    if a:curNo == a:lastNo
        return a:curNo
    endif

    "@ var number å­˜å‚¨ dS ç¬¦å·å¼€å§‹çš„è¡Œå·
    let temp = []

    "@ var nimner ä»¥ dS ç¬¦å·å¼€å§‹çš„è¡Œæ•°é‡
    let scaler = 0

    let incre = a:curNo + 1

    while incre
        let curStr = getline(incre)
        " ''' line
        if matchstr(curStr, '^\s*'. a:dS. '\s*\S\+\s*') != ""
            break
        endif

        " line '''
        if matchstr(curStr, '\S\+\s*'. a:dS .'\s*$') != ""
            if empty(temp)
            " '''
            " line '''
                return incre
            endif

            let scaler += 1
            break
        endif

        if matchstr(curStr, '^\s*'. a:dS) != ""
            call add(temp, incre)
            let scaler += 1
        endif

        let incre += 1

        if incre > a:lastNo
            return a:lastNo
        endif
    endwhile

    if scaler % 2 == 0
        return a:curNo
    else
        return temp[0]
    endif
endfunction
"}}}

" s:GetHeadChar() {{{
"
" è·å–è¡Œé¦–çš„å­—ç¬¦
" @param string haystack æŸä¸€è¡Œçš„å†…å®¹å­—ç¬¦ä¸²
" @param number lth å•ç¬¦å·æˆ–é¦–åŒç¬¦å·é•¿åº¦
" @return string è¿”å›å’Œå•ç¬¦å·æˆ–é¦–åŒç¬¦å·ç›¸åŒé•¿åº¦çš„å­—ç¬¦
"
function s:GetHeadChar(haystack, lth)
    return strpart(substitute(a:haystack, '^\s*', "", ""), 0, a:lth)
endfunction
"}}}

" s:GetTailChar() {{{
"
" è·å–è¡Œå°¾çš„å­—ç¬¦
" @see <SID>GetHeadChar()
" @param string haystack
" @param string dE å°¾åŒç¬¦å·
" @return string è¿”å›å’Œå°¾åŒç¬¦å·ç›¸åŒé•¿åº¦çš„å­—ç¬¦
"
function s:GetTailChar(haystack, dE)
    let dE = escape(a:dE, '\/')
    let haystack = matchstr(a:haystack, dE.'\s*$')
    return strpart(haystack, 0, b:dTailLen)
endfunction
"}}}

" s:GetSmallBlankWidth(){{{

" è·å–æœ€å¤§æˆ–æœ€å°ç©ºç™½å®½åº¦
" @param number curNo  é¦–è¡Œå·
" @param number limit  æœ«è¡Œå·åŠ  1ï¼Œä½œä¸ºå¾ªç¯ç•Œé™ä½¿ç”¨ã€‚
" @return list  [number width, bool flag]
"   width å¦‚æœéƒ½æ˜¯ç©ºç™½è¡Œï¼Œè¿”å›æœ€å¤§çš„ç©ºç™½å®½åº¦ã€‚å¦åˆ™ä¸ºè¿”å›æœ€å°çš„ç©ºç™½å®½åº¦ã€‚å¦‚æœç¬¬
"          ä¸€åˆ—æ¨¡å¼è¢«å¯ç”¨å¹¶æ­£ç¡®åŒ¹é…åˆ°ã€‚æ°¸è¿œè¿”å›0ã€‚
"   flag é¢„ç•™è®¾ç½®
"
function s:GetSmallBlankWidth(curNo, limit)
    " @var list temp_1 æ¯ä¸€ä¸ªéç©ºç™½è¡Œçš„é•¿åº¦
    " @var list temp_2 æ¯ä¸€ä¸ªç©ºç™½è¡Œçš„é•¿åº¦
    let temp_1 = []
    let temp_2 = []

    let incre = a:curNo
    let limit = a:limit

    while incre < limit
        let curStr = getline(incre)

        let blankLine = <SID>IsBlankLine(incre)

        " è¿‡æ»¤å¤šè¡Œä¸­çš„ç©ºè¡Œæˆ–ç©ºç™½è¡Œï¼Œå…è®¸å•è¡Œæ˜¯ç©ºè¡Œæˆ–ç©ºç™½è¡Œ
        if blankLine && a:curNo != a:limit - 1
            let blankLineLen = strlen(curStr)
            let temp_2 = add(temp_2, blankLineLen)
            let incre += 1
            continue
        endif

        let totalLen = strlen(curStr)
        let diff = totalLen - strlen(substitute(curStr, '^\s*', "", "" ))

        let temp_1 = add(temp_1, diff)
        let incre += 1
    endwhile

    if  len(temp_2) == incre - a:curNo
        return [max(temp_2), 0]
    else
        " è¿”å›æœ€å°ç©ºç™½å®½åº¦ã€‚å¦‚æœæ“ä½œçš„æ˜¯å•è¡Œï¼Œä»…æœ‰çš„ä¸€ä¸ªå€¼è¢«è¿”å›
        return [min(temp_1), 1]
    endif
endfunction
"}}}

" s:RestoreCursor(){{{
"
" æ¢å¤å…‰æ ‡ä½ç½®
" @param list cursor åŸå…‰æ ‡ä½ç½®ä¿¡æ¯
" @return void
"
function s:RestoreCursor(curNo, endNo, cursor)
    " å½“å–æ¶ˆæ³¨é‡Šæ“ä½œåå…‰æ ‡çš„ä½ç½®ä¼šå‘ç”Ÿå˜åŒ–ï¼Œå½“å…‰æ ‡åœ¨é¦–ç¬¦å·è¡Œæ—¶å…‰æ ‡ä½ç½®ä¸å˜ã€‚å½“
    " å…‰æ ‡åœ¨å°¾ç¬¦å·è¡Œæ—¶éœ€è¦å‡å»ä¸¤è¡Œã€‚å½“å…‰æ ‡åœ¨é¦–å’Œå°¾ä¹‹é—´çš„è¡Œæ—¶éœ€è¦å‡å»ä¸€è¡Œã€‚

    let temp = a:cursor[1]

    if temp == a:endNo
        let a:cursor[1] -= 2
    endif

    if temp > a:curNo && temp < a:endNo
        let a:cursor[1] -=1
    endif

    call setpos('.', a:cursor)
endfunction
"}}}

" s:PadSpace(){{{
"
" å¡«å……åŒç¬¦å·çš„é¦–è¡Œæˆ–æœªè¡Œ
" @param number input éœ€è¦æ“ä½œçš„è¡Œå·
" @param string char å·²ç»è¢«è½¬ä¹‰è¿‡çš„é¦–ç¬¦å·æˆ–å°¾ç¬¦å·
" @param number width ä»è¿™ä¸ªå®½åº¦ä½ç½®ä¹‹åæ’å…¥ï¼Œå¦‚æœå®½åº¦ä¸º0ï¼Œåˆ™ä»ç¬¬ä¸€åˆ—æ’å…¥ã€‚
"
function s:PadSpace(input, char, width)
    let temp = ""
    let incre = 0

    while incre < a:width
        let temp .= "\x20"
        let incre = strlen(temp)
    endwhile

    execute a:input .'s/^.*$/'. temp . a:char
endfunction
"}}}

" s:IsPatternFirstMode(){{{
"
" åŒ¹é…ç¬¬ä¸€åˆ—æ¨¡å¼å…è®¸çš„æ–‡ä»¶ç±»å‹
" @return bool
"
function s:IsPatternFirstMode()
    if s:isFirstInsert
        for item in split(g:allow_first_column_mode, ',')
            if item == &filetype
                return 1
            endif
        endfor
    endif

    return 0
endfunction
"}}}

" s:IsPatternSemiBlockMode(){{{
"
" åŒ¹é…ç¬¬ä¸€åˆ—æ¨¡å¼å…è®¸çš„æ–‡ä»¶ç±»å‹
" @return bool
"
function s:IsPatternSemiBlockMode()
    if s:isSemiBlock
        for item in split(g:allow_semi_block_mode, ',')
            if item == &filetype
                return 1
            endif
        endfor
    endif

    return 0
endfunction
"}}}

" s:IsPatternEachLineMode(){{{
"
" åŒ¹é…ç¬¬ä¸€åˆ—æ¨¡å¼å…è®¸çš„æ–‡ä»¶ç±»å‹
" @return bool
"
function s:IsPatternEachLineMode()
    if s:isEachLineInsert
        for item in split(g:allow_each_line_mode, ',')
            if item == &filetype
                return 1
            endif
        endfor
    endif

    return 0
endfunction
"}}}

" æ£€æµ‹ä»…æœ‰é¦–ç¬¦å·çš„è¡Œæ˜¯å¦ä»¥é¦–ç¬¦å·æˆ–ç©ºç™½ç»“å°¾
"
" @param number curNo é¦–è¡Œå·
" @param string dS é¦–åŒç¬¦å·
" @param bool strict ä¸ºçœŸæ—¶ï¼Œä»…é¦–ç¬¦å·çš„è¡Œå¹¶ä¸”å¿…é¡»ä»¥é¦–ç¬¦å·ç»“å°¾ã€‚ä¸ºå‡æ—¶ä»…æœ‰é¦–ç¬¦
"                    å·çš„è¡Œå¹¶ä¸”å¿…é¡»æ˜¯ä»¥é¦–ç¬¦å·æˆ–ç©ºç™½å­—ç¬¦ç»“å°¾
" @return bool
"
function s:IsHeadSymEnd(curNo, dS, strict)
    let dS = escape(a:dS, '\/')
    let curStr = getline(a:curNo)

    if a:strict
        return matchstr(curStr, '\V\^\s\*'. dS .'\$') != ''
    else
        return matchstr(curStr, '\V\^\s\*'. dS .'\s\*\$') != ''
    endif
endfunction

" æ£€æµ‹å°¾ç¬¦å·è¡Œæ˜¯å¦ä»¥å°¾ç¬¦å·ç»“å°¾
"
function s:IsTailSymEnd(endNo, dE, strict)
    let dE = escape(a:dE, '\/')
    let endStr = getline(a:endNo)

    if a:strict
        return matchstr(endStr, '\V\^\s\*'. dE .'\$') != ''
    else
        return matchstr(endStr, '\V\^\s\*'. dE .'\s\*\$') != ''
    endif
endfunction

" æ˜¯å¦å½“å‰è¡Œåªæœ‰å‰è¾å­—ç¬¦
" @return bool
"
function s:IsOnlyPrefix(curNo)
    let prefix = escape(substitute(s:isLinePrefix, '\s*$', "", ""), '\/')
    return matchstr(getline(a:curNo), '\V\^\s\*'. prefix .'\s\*\$') != ''
endfunction

" s:IsStrictFormat(){{{
"
" æ˜¯å¦ä¸ºä¸¥æ ¼çš„åˆ—å—æ³¨é‡Šæ ¼å¼
" @param number curNo é¦–è¡Œå·
" @param number endNo æœ«è¡Œå·
" @return list [bool format, string error_code]
"   format å¦‚æœæ˜¯åˆ—å—çš„æ³¨é‡Šæ ¼å¼ä¸ºçœŸ
"   error_code é¢„ç•™é”™è¯¯æ¶ˆæ¯å†…éƒ¨è°ƒè¯•
"
function s:IsStrictFormat(curNo, endNo, dS, dE)
    let headStr = getline(a:curNo)
    let tailStr = getline(a:endNo)

    " æ€»é•¿åº¦
    let headLen = strlen(headStr)
    let tailLen = strlen(tailStr)

    " å»æ‰å¼€å§‹ç©ºç™½å­—ç¬¦åçš„é•¿åº¦
    let beforeHeadLen = strlen(substitute(headStr, '^\s*', "", ""))
    let beforeTailLen = strlen(substitute(tailStr, '^\s*', "", ""))

    " ç©ºç™½é•¿åº¦
    let headBlankLen = headLen - beforeHeadLen
    let tailBlankLen = tailLen - beforeTailLen

    if headBlankLen
        let baseWidth = headBlankLen
        let tailWidth = baseWidth + 1
        let otherWidth = baseWidth + 1
    else
        let baseWidth = 0
        let tailWidth = 1
        let otherWidth = 1
    endif

    " åªæ£€æŸ¥å°¾ç¬¦å·è¡Œçš„ç©ºç™½å®½åº¦å³å¯ï¼Œå› ä¸ºæ˜¯é€šè¿‡é¦–ç¬¦å·è¡Œè®¡ç®—çš„ã€‚
    if tailBlankLen != tailWidth | return [0, 'DE1'] | endif

    " æ£€æŸ¥é¦–å’Œå°¾ç¬¦å·è¡Œæ˜¯å¦ä»¥é¦–å’Œå°¾ç¬¦å·ç»“å°¾
    let ihse = <SID>IsHeadSymEnd(a:curNo, a:dS, 1)
    let itse = <SID>IsTailSymEnd(a:endNo, a:dE, 1)

    if !ihse | return [0, 'DE2'] | endif
    if !itse | return [0, 'DE2'] | endif

    let incre = a:curNo + 1

    " æ£€æŸ¥å†…è¡Œ
    while incre < a:endNo
        let inLineStr = getline(incre)
        let inLineLen = strlen(inLineStr)

        let beforeInLineStr = substitute(inLineStr, '^\s*', "", "")
        let beforeInLineLen = strlen(beforeInLineStr)
        let inLineBlankLen = inLineLen - beforeInLineLen

        if s:isLinePrefix != ''
            let prefix = strpart(beforeInLineStr, 0, b:prefixLen)
            let iop = <SID>IsOnlyPrefix(incre)

            " ä¸æ£€æŸ¥åªæœ‰å‰è¾çš„è¡Œ
            if iop
                let incre += 1
                continue
            endif

            " å†…è¡Œå‰è¾å­—ä¸²æ˜¯å¦ç›¸åŒ
            if prefix != s:isLinePrefix
                return [0, 'DE3']
            endif
        else
            let incre += 1
            continue
        endif

        " æ£€æŸ¥å†…è¡Œæœ€å°ç©ºç™½å®½åº¦
        if inLineBlankLen != otherWidth
            return [0, 'DE4']
        endif

        let incre += 1
    endwhile

    return [1, "TODO"]

    " @var list TODO
    " let inLinePrefixCell = []
endfunction
"}}}

" æ˜¯å¦ä¸ºç©ºç™½è¡Œ
" @return bool å¦‚æœæ˜¯ç©ºè¡Œæˆ–ç©ºç™½è¡Œä¸ºçœŸ
"
function s:IsBlankLine(curNo)
    let char = matchstr(getline(a:curNo), '\S')

    if char != ""
        return 0
    else
        return 1
    endif
endfunction


" TODO å˜é‡ g:add_comment_symbol çº é”™
"
" function s:IsValueCorrect()
" endfunction

" é”™è¯¯è¾“å‡º
"
function s:ErrorHandle(level, index)
    echohl ErrorMsg | echo s:ERROR_CODE[a:level][a:index] | echohl None
endfunction

let &cpo = s:save_cpo
unlet s:save_cpo

" vim:ft=vim:ff=unix:ts=4:et:fdm=marker:
